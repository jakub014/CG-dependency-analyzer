plugins {
    id 'groovy'
    id 'java'
    id 'com.github.johnrengelman.shadow' version "6.0.0"
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.asseco-pst'
version = '0.0.6'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy:3.0.5'
    implementation 'com.ibm.websphere.appserver.api:com.ibm.websphere.appserver.api.basics:1.4.43'
    implementation 'com.ibm.websphere.appserver.api:com.ibm.websphere.appserver.api.config:1.2.43'
    implementation 'com.ibm.websphere.appserver.api:com.ibm.websphere.appserver.api.endpoint:1.0.43'
    implementation 'com.ibm.websphere.appserver.api:com.ibm.websphere.appserver.api.kernel.service:1.0.43'
    implementation 'com.ibm.websphere.appserver.api:com.ibm.websphere.appserver.api.restConnector:1.3.44'
    implementation fileTree(dir: 'libs', include: ['restConnector.jar'])
}

shadowJar {
    classifier = ''
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        shadow(MavenPublication) {
            artifactId = 'liberty'
            project.shadow.component(it)
            pom {
                name = 'liberty'
                description = 'A library that allows managing artifacts in a Liberty server'
                url = 'https://github.com/asseco-pst/liberty'
                inceptionYear = '2020'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'assecopst'
                        name = 'Asseco PST'
                        email = 'servicos.dcs@pst.asseco.com'
                        organization = 'Asseco PST'
                        organizationUrl = 'https://www.pst.asseco.com'
                    }
                }
                scm {
                    url = 'https://github.com/asseco-pst/liberty'
                    connection = 'scm:https://github.com/asseco-pst/liberty.git'
                    developerConnection = 'scm:git://github.com/asseco-pst/liberty.git'
                }
            }
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    sign publishing.publications.shadow
}