import org.gradle.internal.os.OperatingSystem

import java.time.Duration

plugins {
    id 'java'
    id 'java-library'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    id 'maven-publish'
    id 'signing'
    // check dependency updates: ./gradlew dependencyUpdates -Drevision=release
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.github.stephenc.jcip:jcip-annotations:1.0-1'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.9.0'
}

group = 'nl.stokpop'
version = '3.0.1'
description = 'event-scheduler'
sourceCompatibility = '1.8'

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

license {
    // license check is broken on windows, skip
    if (OperatingSystem.current().isWindows()) ignoreFailures = true
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Peter Paul Bakker, Stokpop Software Solutions'

    header file("src/main/resources/META-INF/LICENSE.txt")

    excludes(["**/*.txt","src/main/resources/META-INF/services/**"])

    mapping {
        // the standard java style makes intellij complain about dangling javadoc
        java = "SLASHSTAR_STYLE"
    }

    ignoreFailures false
    strictCheck true
}

nexusPublishing {
    repositories {
        sonatype()
    }
    connectTimeout = Duration.ofMinutes(10)
    clientTimeout = Duration.ofMinutes(10)
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar) {
            archiveClassifier = 'javadoc' as Property<String>
            from javadoc
        }

        task sourcesJar(type: Jar) {
            archiveClassifier = 'sources' as Property<String>
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    String repositoryUrl
                    if (version.endsWith("SNAPSHOT")) {
                        repositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                    }
                    else {
                        repositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    }
                    repository(url: repositoryUrl) {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/stokpop/event-scheduler.git'

                        scm {
                            connection 'scm:git:https://github.com/stokpop/event-scheduler.git'
                            developerConnection 'scm:git:git@github.com:stokpop/event-scheduler.git'
                            url 'https://github.com/stokpop/event-scheduler.git'
                        }

                        licenses {
                            license {
                                name "The Apache Software License, Version 2.0"
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'stokpop'
                                name = 'Peter Paul Bakker'
                                email = 'peter.paul.bakker@stokpop.nl'
                            }
                        }
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = project.properties['ossrhUser'] ?: "noUser"
    password = project.properties['ossrhPassword'] ?: "noPassword"
    packageGroup = "nl.stokpop"
    stagingProfileId = project.properties['ossrhStagingProfileId'] ?: ""
}
