apply plugin: 'maven'
apply plugin: 'osgi'
apply plugin: 'signing'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

version = '2.12.0'
group = 'de.undercouch'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) { 
    options.compilerArgs << "-Xlint" 
} 

configurations {
    integrationTestBase
    integrationTest21 { extendsFrom integrationTestBase }
    integrationTest22 { extendsFrom integrationTestBase }
    integrationTest23 { extendsFrom integrationTestBase }
    integrationTest24 { extendsFrom integrationTestBase }
    integrationTest25 { extendsFrom integrationTestBase }
    integrationTest26 { extendsFrom integrationTestBase }
    integrationTest27 { extendsFrom integrationTestBase }
    integrationTest28 { extendsFrom integrationTestBase }
    integrationTest29 { extendsFrom integrationTestBase }
    integrationTest210 { extendsFrom integrationTestBase }
    integrationTest211 { extendsFrom integrationTestBase }
    integrationTest212 { extendsFrom integrationTestBase }
}

String jacksonCore = 'com.fasterxml.jackson.core:jackson-core:'
String jacksonAnnotations = 'com.fasterxml.jackson.core:jackson-annotations:'
String jacksonDatabind = 'com.fasterxml.jackson.core:jackson-databind:'
String junit = 'junit:junit:4.13'
String mongoDbJavaDriver = 'org.mongodb:mongo-java-driver:3.12.6'

dependencies {
    compile jacksonCore + '2.12.1'
    compile jacksonAnnotations + '2.12.1'
    compile jacksonDatabind + '2.12.1'

    testCompile junit
    testCompile mongoDbJavaDriver
    
    integrationTestBase junit
    integrationTestBase mongoDbJavaDriver
        
    integrationTest21 jacksonCore + '2.1.0'
    integrationTest21 jacksonAnnotations + '2.1.0'
    integrationTest21 jacksonDatabind + '2.1.0'
    
    integrationTest22 jacksonCore + '2.2.0'
    integrationTest22 jacksonAnnotations + '2.2.0'
    integrationTest22 jacksonDatabind + '2.2.0'
    
    integrationTest23 jacksonCore + '2.3.0'
    integrationTest23 jacksonAnnotations + '2.3.0'
    integrationTest23 jacksonDatabind + '2.3.0'
    
    integrationTest24 jacksonCore + '2.4.0'
    integrationTest24 jacksonAnnotations + '2.4.0'
    integrationTest24 jacksonDatabind + '2.4.0'
    
    integrationTest25 jacksonCore + '2.5.0'
    integrationTest25 jacksonAnnotations + '2.5.0'
    integrationTest25 jacksonDatabind + '2.5.0'
    
    integrationTest26 jacksonCore + '2.6.4'
    integrationTest26 jacksonAnnotations + '2.6.4'
    integrationTest26 jacksonDatabind + '2.6.4'

    integrationTest27 jacksonCore + '2.7.0'
    integrationTest27 jacksonAnnotations + '2.7.0'
    integrationTest27 jacksonDatabind + '2.7.0'

    integrationTest28 jacksonCore + '2.8.0'
    integrationTest28 jacksonAnnotations + '2.8.0'
    integrationTest28 jacksonDatabind + '2.8.0'

    integrationTest29 jacksonCore + '2.9.3'
    integrationTest29 jacksonAnnotations + '2.9.3'
    integrationTest29 jacksonDatabind + '2.9.3'

    integrationTest210 jacksonCore + '2.10.5'
    integrationTest210 jacksonAnnotations + '2.10.5'
    integrationTest210 jacksonDatabind + '2.10.5'

    integrationTest211 jacksonCore + '2.11.1'
    integrationTest211 jacksonAnnotations + '2.11.1'
    integrationTest211 jacksonDatabind + '2.11.1'

    integrationTest212 jacksonCore + '2.12.1'
    integrationTest212 jacksonAnnotations + '2.12.1'
    integrationTest212 jacksonDatabind + '2.12.1'
}

jar {
    // define OSGi bundle manifest
    manifest {
        name = 'bson4jackson'
        symbolicName = "de.undercouch.bson4jackson"
        vendor = 'Michel Kraemer'
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "de.undercouch.*;version=${version}"
    }
    
    // include license into jar
    from 'LICENSE.txt'
}

// package javadoc into a jar file
task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// package source into a jar file
task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task doIntegrationTest21(type: Test) {
    useJUnit {
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_3'
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_5'
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_7'
    }

    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest21
}

task doIntegrationTest22(type: Test) {
    useJUnit {
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_3'
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_5'
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_7'
    }

    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest22
}

task doIntegrationTest23(type: Test) {
    useJUnit {
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_5'
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_7'
    }

    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest23
}

task doIntegrationTest24(type: Test) {
    useJUnit {
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_5'
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_7'
    }

    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest24
}

task doIntegrationTest25(type: Test) {
    useJUnit {
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_7'
    }
    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest25
}

task doIntegrationTest26(type: Test) {
    useJUnit {
        excludeCategories 'de.undercouch.bson4jackson.RequiresJackson_v2_7'
    }
    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest26
}

task doIntegrationTest27(type: Test) {
    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest27
}

task doIntegrationTest28(type: Test) {
    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest28
}

task doIntegrationTest29(type: Test) {
    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest29
}

task doIntegrationTest210(type: Test) {
    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest210
}

task doIntegrationTest211(type: Test) {
    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest211
}

task doIntegrationTest212(type: Test) {
    classpath = sourceSets.test.output + sourceSets.main.output + configurations.integrationTest212
}

task integrationTest(dependsOn: [ doIntegrationTest21, doIntegrationTest22,
        doIntegrationTest23, doIntegrationTest24, doIntegrationTest25,
        doIntegrationTest26, doIntegrationTest27, doIntegrationTest28,
        doIntegrationTest29, doIntegrationTest210, doIntegrationTest211,
        doIntegrationTest212 ])

check.dependsOn integrationTest

// define artifacts for upload
artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}


// sign all artifacts
signing {
    required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask(':uploadArchives') }
    sign configurations.archives
}

// remove test dependencies from configuration-to-scope mapping
// this also removes them from the maven pom file
conf2ScopeMappings.mappings.remove(configurations.testCompile)

uploadArchives {
    repositories {
        mavenDeployer {
            // sign artifacts before upload
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            
            // upload to sonatype OSS
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: this.hasProperty('sonatypeUsername') ? sonatypeUsername : '',
                    password: this.hasProperty('sonatypePassword') ? sonatypePassword : '')
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: this.hasProperty('sonatypeUsername') ? sonatypeUsername : '',
                    password: this.hasProperty('sonatypePassword') ? sonatypePassword : '')
            }
            
            // pom file details
            pom.project {
               name 'bson4jackson'
               packaging 'jar'
               description 'A pluggable BSON generator and parser for Jackson JSON processor.'
               url 'https://michelkraemer.com'
 
               scm {
                   url 'scm:git:git://github.com/michel-kraemer/bson4jackson.git'
                   connection 'scm:git:git://github.com/michel-kraemer/bson4jackson.git'
                   developerConnection 'scm:git:git://github.com/michel-kraemer/bson4jackson.git'
               }
 
               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }
 
               developers {
                   developer {
                       id 'michel-kraemer'
                       name 'Michel Kraemer'
                       email 'michel@undercouch.de'
                   }
               }
           }
        }
    }
}
