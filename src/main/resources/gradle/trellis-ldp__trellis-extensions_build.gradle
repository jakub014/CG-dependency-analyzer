plugins {
    id 'com.github.ben-manes.versions' version "0.38.0"
    id 'com.github.hierynomus.license' version "0.15.0"
    id 'net.researchgate.release' version "2.8.1"
    id 'org.gradle.test-retry' version "1.2.1"
    id 'org.owasp.dependencycheck' version "6.1.6"
    id 'org.sonarqube' version "3.2.0"
}

ext {

    // Trellis core
    trellisVersion = "0.18.0"

    // JavaEE
    activationApiVersion = "1.2.2"
    annotationApiVersion = "1.3.5"
    cdiApiVersion = "2.0.2"
    injectApiVersion = "1.0.1"
    jaxbApiVersion = "2.3.3"
    jmsApiVersion = "2.0.3"
    transactionApiVersion = "1.3.3"

    // Miroprofile
    microprofileConfigVersion = "1.4"
    microprofileHealthVersion = "2.2"

    // Dependencies
    awsVersion = "1.11.1015"
    commonsCompressVersion = "1.20"
    commonsIoVersion = "2.8.0"
    commonsLangVersion = "3.10"
    commonsRdfVersion = "0.5.0"
    guavaVersion = "30.1.1-jre"
    jacksonVersion = "2.10.1"
    jenaVersion = "4.0.0"
    mustacheVersion = "0.9.7"
    quarkusVersion = "1.13.3.Final"
    slf4jVersion = "1.7.30"
    smallryeConfigVersion = "2.2.0"
    smallryeHealthVersion = "2.2.5"
    snakeyamlVersion = "1.28"

    // Databases
    cassandraDriverVersion = "4.10.0"

    // Testing
    awaitilityVersion = "4.1.0"
    commonsTextVersion = "1.9"
    junitVersion = "5.7.1"
    logbackVersion = "1.2.3"
    mockitoVersion = "3.9.0"
    weldVersion = "2.0.2.Final"

    // Tools
    checkstyleToolVersion = "8.34"
    jacocoToolVersion = "0.8.6"
    pmdToolVersion = "6.24.0"

    jacocoSkipProjects = [
        'trellis-ext-bom'
    ]

    omitFromMavenPublishing = [
        'trellis-quarkus'
    ]
}

allprojects { subproj ->
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'org.gradle.test-retry'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'

    ext {
        vendor = 'Trellis LDP'
        homepage = 'https://www.trellisldp.org/'
        docURL = 'https://www.trellisldp.org/docs/trellis-extensions/current/apidocs/'
        license = 'Apache-2.0'
    }

    jacoco.toolVersion = jacocoToolVersion

    group = 'org.trellisldp.ext'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {
        testImplementation enforcedPlatform("org.junit:junit-bom:$junitVersion")
        testImplementation("org.junit.jupiter:junit-jupiter-api")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    release {
        tagTemplate = '$name-$version'
        git {
            /* Note: for maintenance branches, set this value to the appropriate branch name. */
            requireBranch = 'main'
            signTag = true
        }
    }

    configurations.all {
        resolutionStrategy {
            // Use a newer version of snakeyaml (via Jackson)
            force "org.yaml:snakeyaml:$snakeyamlVersion"
        }
        resolutionStrategy.dependencySubstitution {
            substitute module("org.apache.geronimo.specs:geronimo-annotation_1.2_spec") with module ("jakarta.annotation:jakarta.annotation-api:$annotationApiVersion")
            substitute module("org.jboss.spec.javax.transaction:jboss-transaction-api_1.2_spec") with module ("jakarta.transaction:jakarta.transaction-api:$transactionApiVersion")
            substitute module("javax.annotation:javax.annotation-api") with module("jakarta.annotation:jakarta.annotation-api:$annotationApiVersion")
            substitute module("javax.enterprise:cdi-api") with module("jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion")
            substitute module("javax.inject:javax.inject") with module("jakarta.inject:jakarta.inject-api:$injectApiVersion")
            substitute module("javax.xml.bind:jaxb-api") with module("jakarta.xml.bind:jakarta.xml.bind-api:$jaxbApiVersion")
        }
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group.startsWith('com.fasterxml.jackson.')) {
                details.useVersion jacksonVersion
                details.because 'Jackson modules should use a consistent version'
            }
        }
    }
}

subprojects { subproj ->

    sourceCompatibility = 11
    targetCompatibility = 11

    jar {
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
    }

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleToolVersion
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from javadoc
    }

    task sourceJar(type: Jar) {
        classifier 'sources'
        from("$rootDir/LICENSE") {
            into "META-INF"
        }
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar
        archives sourceJar
    }

    license {
        include "**/*.java"
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    downloadLicenses {
        includeProjectDependencies = true
        dependencyConfiguration = 'compileClasspath'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                pom {
                    packaging = 'jar'
                    name = 'Trellis Linked Data Server: database extension'
                    description = 'A database persistence layer for the Trellis linked data server'
                    url = "https://www.trellisldp.org"
                    inceptionYear = '2017'

                    organization {
                        name = project.vendor
                        url = project.homepage
                    }

                    developers {
                        developer {
                            id = 'acoburn'
                            name = 'Aaron Coburn'
                            email = 'acoburn (at) apache (dot) org'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/trellis-ldp/trellis-extensions.git'
                        developerConnection = 'scm:git:git@github.com/trellis-ldp/trellis-extensions.git'
                        url = 'https://github.com/trellis-ldp/trellis-extensions'
                        tag = 'HEAD'
                    }

                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            comments = 'Copyright (c) 2017-2020 Aaron Coburn and individual contributors'
                            distribution = 'repo'
                        }
                    }
                }
                pom.withXml {
                    // eliminate test-scoped dependencies
                    asNode().dependencies.removeAll { dep -> dep.scope == "test" }
                    // use provided scope for JavaEE dependencies
                    asNode().dependencies.each {
                        deps -> deps.findAll { dep -> dep.groupId[0].text().startsWith("jakarta.") }
                                    .each { dep -> dep.scope[0].value = "provided" } }
                }

                from components.java

                artifact(sourceJar) {
                    classifier = 'sources'
                }

                artifact(javadocJar) {
                    classifier = 'javadoc'
                }
            }
        }
        repositories {
            maven {
                def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv("SONATYPE_USERNAME") ?: ""
                def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv("SONATYPE_PASSWORD") ?: ""
                if (version.endsWith("SNAPSHOT")) {
                    url "https://oss.sonatype.org/content/repositories/snapshots/"
                } else {
                    url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                }
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    tasks.withType(PublishToMavenRepository) {
        onlyIf {
            ! omitFromMavenPublishing.contains(subproj.name)
        }
    }
    tasks.withType(PublishToMavenLocal) {
        onlyIf {
            ! omitFromMavenPublishing.contains(subproj.name)
        }
    }
    task install(dependsOn: [assemble, publishToMavenLocal])
    task upload(dependsOn: [assemble, publish])

    tasks.withType(Sign) {
        onlyIf { !version.endsWith("SNAPSHOT") }
    }

    processResources {
        outputs.upToDateWhen { false }
        filesMatching(['**/features.xml', '**/banner.txt']) {
            expand project.properties
        }
    }

    signing {
        required {
            !version.toString().endsWith('-SNAPSHOT') && tasks.withType(PublishToMavenRepository).find {
                gradle.taskGraph.hasTask it
            }
        }
        sign publishing.publications
    }

    gradle.taskGraph.whenReady( { graph ->
        tasks.withType(AbstractArchiveTask).each { archive ->
            archive.doLast {
                ant.checksum file: archive.archivePath, algorithm: 'sha-256', format: 'MD5SUM', fileext: '.sha256'
            }
        }
    })

    javadoc {
        failOnError = false
        options.addBooleanOption('html5', true)
        options {
            tags = ["apiNote:a:API Note:",
                    "implSpec:a:Implementation Requirements:",
                    "implNote:a:Implementation Note:"]
            links "https://docs.oracle.com/en/java/javase/11/docs/api/"
            links 'https://jakarta.ee/specifications/platform/8/apidocs/'
            links "https://www.trellisldp.org/docs/trellis/current/apidocs/"
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://docs.datastax.com/en/drivers/java/4.0/'
            links 'https://docs.aws.amazon.com/AWSJavaSDK/latest/javadoc/'
        }
    }

    sonarqube {
        // Only record this for jdk11 builds
        skipProject = ! JavaVersion.current().isJava11()
    }

    pmd {
        ignoreFailures = true
        consoleOutput = true
        incrementalAnalysis = true
        toolVersion = pmdToolVersion
        sourceSets = [sourceSets.main]
        rulePriority = 4
        ruleSets = []
        ruleSetConfig = resources.text.fromFile(rootProject.file('buildtools/src/main/resources/pmd/pmd.xml'))
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat "full"
            showStackTraces false
        }
        retry {
            maxRetries = (project.findProperty("trellis.test.retry") ?: "3").toInteger()
            maxFailures = 20
        }
    }

    afterReleaseBuild.dependsOn publish
    afterReleaseBuild.dependsOn assemble

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

configure(rootProject) {

    task apidocs(type: Javadoc) {
        failOnError false
        outputs.upToDateWhen { false }
        destinationDir = new File(projectDir, "docs/apidocs/${project.version}")
        title = "Trellis Linked Data Server Documentation"
        exclude '**/impl/*'
        exclude '**/*Tests.java'
        exclude '**/module-info.java'
        options {
            tags = ["apiNote:a:API Note:",
                "implSpec:a:Implementation Requirements:",
                "implNote:a:Implementation Note:"]
            memberLevel = JavadocMemberLevel.PUBLIC
            links "https://docs.oracle.com/en/java/javase/11/docs/api/"
            links 'https://jakarta.ee/specifications/platform/8/apidocs/'
            links "https://www.trellisldp.org/docs/trellis/current/apidocs/"
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
            links 'https://docs.datastax.com/en/drivers/java/4.0/'
            links 'https://docs.aws.amazon.com/AWSJavaSDK/latest/javadoc/'
        }

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }

        source subprojects.collect { project -> project.sourceSets.main.allJava }
        classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
    }

    sonarqube {
        properties {
            property "sonar.projectName", "Trellis Linked Data Server Extensions"
            property "sonar.projectKey", "org.trellisldp:trellis-extensions"
            property "sonar.links.homepage", "https://www.trellisldp.org"
            property "sonar.links.issue", "https://github.com/trellis-ldp/trellis-extensions/issues"
            property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis-extensions.git"
            property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
        }
    }

    // Ignore alpha, beta, milestone and release candidates
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'm', 'cr'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject("Release Candidate")
                }
            }
        }
    }

    task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
        dependsOn = subprojects.test
        additionalSourceDirs.from(files(subprojects.sourceSets.main.allSource.srcDirs))
        sourceDirectories.from(files(subprojects.sourceSets.main.allSource.srcDirs))
        classDirectories.from(files(subprojects.sourceSets.main.output))
        executionData.from(files(subprojects.findAll { ! jacocoSkipProjects.contains(it.name) }.jacocoTestReport.executionData))
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }

    task getVersion {
        doLast {
            println project.version
        }
    }
}
