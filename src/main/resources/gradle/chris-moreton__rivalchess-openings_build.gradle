buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'maven-publish'
}

group = "com.netsensia.rivalchess"
version = '1.0.5'

jar.baseName("rivalchess-openings")

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'rivalchess-openings'
                packaging 'jar'
                description 'A simple opening library used by Rival Chess'
                url 'https://github.com/chris-moreton/rivalchess-openings'

                scm {
                    connection = 'scm:git:git://github.com/chris-moreton/rivalchess-openings.git'
                    developerConnection = 'scm:git:ssh://github.com/chris-moreton/rivalchess-openings.git'
                    url = 'https://github.com/chris-moreton/rivalchess-openings'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'chris-moreton'
                        name = 'Chris Moreton'
                        email = 'chris@netsensia.com'
                    }
                }
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile group: 'com.netsensia.rivalchess', name: 'rivalchess-model', version: '7.0.3'
    testImplementation 'junit:junit:4.12'
}

test {
    useJUnit()
    maxHeapSize = '1G'
    testLogging.showStandardStreams = true
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}
