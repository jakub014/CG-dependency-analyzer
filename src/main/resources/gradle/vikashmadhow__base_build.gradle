/*
 * Copyright (c) 2016 Vikash Madhow
 */

/**
 * Base algos, data structures and utility functions.
 *
 * @author Vikash Madhow (vikash.madhow@gmail.com)
 */

buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
}

plugins {
  id "java-library"
  id "maven-publish"
  id "signing"
  id 'io.codearte.nexus-staging' version '0.22.0'
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

java {
  modularity.inferModulePath = true
}

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java']
    }
    resources {
      srcDirs = ['src/main/resources']
    }
  }
  test {
    java {
      srcDirs = ['src/test/java']
    }
    resources {
      srcDirs = ['src/test/resources']
    }
  }
}

dependencies {
  // This dependency is exported to consumers, that is to say found on their compile classpath.
  implementation("org.apache.commons:commons-lang3:latest.release")
  implementation("com.fasterxml.jackson.core:jackson-core:latest.release")
  implementation("com.fasterxml.woodstox:woodstox-core:latest.release")
  implementation("org.yaml:snakeyaml:latest.release")

  // Use JUnit test framework
  testImplementation(platform('org.junit:junit-bom:5.7.0'))
  testImplementation('org.junit.jupiter:junit-jupiter:5.7.0')
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

group = 'com.vikmad'
archivesBaseName = 'base'
sourceCompatibility = JavaVersion.VERSION_14
version = "0.3.2"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  modularity.inferModulePath = true
  withJavadocJar()
  withSourcesJar()
}

publishing {
  repositories {
    maven {
      def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
      url = isReleaseVersion ? releaseRepo : snapshotRepo
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        groupId = 'com.vikmad'
        artifactId = 'base'
        name = "Base algos and utils"
        description = 'Base algos, data structures and utilities'
        url = 'https://github.com/vikashmadhow/base'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = "vikashmadhow"
            name = "Vikash Madhow"
            email = "vikash.madhow@gmail.com"
            url = "http://vikmad.com"
          }
        }
        scm {
          url = 'https://github.com/vikashmadhow/base'
          connection = 'scm:https://github.com/vikashmadhow/base.git'
          developerConnection = 'scm:git://github.com/vikashmadhow/base.git'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}

nexusStaging {
  packageGroup = group
  username = project.getProperty('ossrhUsername')
  password = project.getProperty('ossrhPassword')
}
