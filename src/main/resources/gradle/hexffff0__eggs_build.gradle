plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.6.5'
    id 'maven-publish'
    id 'signing'
}

group 'com.github.hexffff0'
version '1.0.4'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.mdkt.compiler:InMemoryJavaCompiler:1.3.0'
    compile group: 'com.github.javaparser', name: 'javaparser-core', version: '3.18.0'
    compile group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

intellij {
    plugins = ['java', 'maven']
//     intellij.version and intellij.localPath should not be specified at the same time.
//     intellij.localPath '/Applications/IntelliJ IDEA.app'
    intellij.version '2020.2.2'
    updateSinceUntilBuild false

}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

javadoc {
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
}

publishing {
    // 定义发布什么
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Eggs'
                description = 'Eggs是一个强大的Intellij IDEA 插件，它允许你在运行时编写Java代码与IDEA进行交互，最终得到你想要的效果，例如生成代码。'
                url = 'https://github.com/hexffff0/eggs'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 开发者信息
                developers {
                    developer {
                        name = 'hyc'
                        email = 'huang_yicheng@foxmail.com'
                    }
                }
                // 版本控制仓库地址
                scm {
                    url = 'https://github.com/hexffff0/eggs'
                    connection = 'https://github.com/hexffff0/eggs.git'
                    developerConnection = 'https://github.com/hexffff0/eggs.git'
                }
            }
        }
    }
    // 定义发布到哪里
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                // 这里就是之前在issues.sonatype.org注册的账号
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}

patchPluginXml {

    sinceBuild 191

    changeNotes """
  <p>version 1.0.4 Fix update ExecuteUnit not working</P>
  <p>version 1.0.3 Add CommonTextEditor</P>
  <p>version 1.0.2 Enable run code on ConsoleEditorPopupMenu</P>
  <p>version 1.0.1 Add English description</P>
  <p>version 1.0.0 First release</P>"""
}

test {
    useJUnitPlatform()
}