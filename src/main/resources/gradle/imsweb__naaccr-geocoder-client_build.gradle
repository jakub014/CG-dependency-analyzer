plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '3.0.0'
    id 'jacoco'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

group = 'com.imsweb'
version = '1.17'
description = 'Java client library for NAACCR Geocoding Service'
sourceCompatibility = JavaVersion.VERSION_1_9
targetCompatibility = JavaVersion.VERSION_1_9

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    api 'com.squareup.retrofit2:retrofit:2.3.0'

    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'junit-addons:junit-addons:1.4'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'com.jcabi:jcabi-matchers:1.3'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-serial" << "-Werror"
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
    manifest {
        attributes('Implementation-Title': 'NAACCR Gecoding Java Client',
                'Implementation-Version': project.version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.geocoder')
    }
}

// checkstyle plugin settings
checkstyle {
    toolVersion = '8.14'
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile': project(':').file('config/checkstyle/suppressions.xml')]
}


// spotbugs plugin settings
spotbugs {
    toolVersion = '3.1.9'
    ignoreFailures = true
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

// configure nexus staging plugin
nexusStaging {
    numberOfRetries = 50
    delayBetweenRetriesInMillis = 5000
}

modifyPom {
    project {
        name 'NAACCR Gecodoer Java Client'
        description 'API mapping for NAACCR Geocoder in Java'
        url 'https://github.com/imsweb/naaccr-geocoder-client'
        inceptionYear '2017'

        scm {
            url 'https://github.com/imsweb/naaccr-geocoder-client'
            connection 'scm:https://github.com/imsweb/naaccr-geocoder-client.git'
            developerConnection 'scm:git@github.com:imsweb/naaccr-geocoder-client.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ctmay4'
                name 'Chuck May'
                email 'mayc@imsweb.com'
            }
            developer {
                id 'criderp'
                name 'Philip Crider'
                email 'criderp@imsweb.com'
            }
            developer {
                id 'jlorentson'
                name 'Jesse Lorentson'
                email 'lorentsonj@imsweb.com'
            }
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames.add('closeAndReleaseRepository')

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '6.0.1'
    distributionType = Wrapper.DistributionType.ALL
}

