plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.diffplug.gradle.spotless" version "3.25.0"
    id 'io.codearte.nexus-staging' version '0.21.1'
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id "de.undercouch.download" version "4.0.0"
}

description 'Web3j-evm extension'

ext {
    web3jVersion = '4.8.4'
    log4jVersion = '2.12.1'
    logbackVersion = '1.2.3'
    guavaVersion = '28.1-jre'
    jacksonVersion = '2.10.0'
    klaxonVersion = '5.0.1'
    kotlinVersion = '1.4.10'
    besuPluginVersion = '20.10.2'
    besuInternalVersion = '20.10.2'
    besuCryptoDepVersion = '0.3.0'
}

apply {
    [
        'bintray',
        'jacoco',
        'java',
        'javadoc',
        'repositories',
        'publish',
        'spotless',
        'junit'
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite false
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}

dependencies {
    implementation "org.web3j:core:$web3jVersion"
    implementation "org.apache.logging.log4j:log4j-to-slf4j:$log4jVersion"
    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    implementation "com.beust:klaxon:$klaxonVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.hyperledger.besu:plugin-api:$besuPluginVersion"
    implementation "org.hyperledger.besu.internal:api:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:config:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:core:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:crypto:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:rlp:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:kvstore:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:metrics-core:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:trie:$besuInternalVersion"
    implementation "org.hyperledger.besu.internal:util:$besuInternalVersion"
    implementation "org.hyperledger.besu:bls12-381:$besuCryptoDepVersion"
    implementation "org.hyperledger.besu:secp256k1:$besuCryptoDepVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
