plugins {
//    id 'groovy'
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.lvonce'
archivesBaseName = 'wind'
version '0.4.6'
description 'wind library'
sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    mavenCentral()
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}


//sourceSets.main.java.srcDirs = []
//sourceSets.main.groovy.srcDirs += ["src/main/java"]
//sourceSets.test.java.srcDirs = []
//sourceSets.test.groovy.srcDirs += ["src/test/java"]
//




publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "wind"
                description = 'wind library'
                url = 'https://github.com/luban-project/wind'

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "thegenius"
                        name = "thegenius"
                        email = "thegenius@vip.qq.com"
                    }
                }
                scm {
                    url = 'https://github.com/luban-project/wind'
                    connection = 'scm:git:git://github.com:luban-project/wind.git'
                    developerConnection = 'scm:git:ssh://git@github.com:luban-project/wind.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            // 如果version以SNAPSHOT结尾就会上传到快照仓库，如果不是就上传到release仓库
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${sonatypeUsername}"
                password = "${sonatypePassword}"
            }
        }

    }
}

signing {
    sign publishing.publications.mavenJava
}


dependencies {

    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.mybatis', name: 'mybatis', version: '3.5.1'
    compile group: 'org.mybatis', name: 'mybatis-spring', version: '2.0.5'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.4'
    compile group: 'com.baidu.unbiz', name: 'fluent-validator', version: '1.0.9'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compile group: 'org.antlr', name: 'antlr4', version: '4.8'
    compile 'io.methvin:directory-watcher:0.10.1'
    compile 'commons-io:commons-io:2.6'
    compile 'com.google.inject:guice:5.0.0-BETA-1'
    compile 'javax.servlet:javax.servlet-api:4.0.1'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.11.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.11.0'

    compile group: 'com.zaxxer', name: 'HikariCP', version: '3.4.5'
    compileOnly 'org.projectlombok:lombok:1.18.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.2'

    testCompileOnly 'org.projectlombok:lombok:1.18.2'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.2'

    runtimeOnly 'com.h2database:h2:1.4.200'
    runtimeOnly 'mysql:mysql-connector-java:8.0.20'
}
