import org.jetbrains.grammarkit.tasks.GenerateParser
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
  apply from: "$rootDir/gradle/dependencies.gradle"

  repositories {
    mavenCentral()
    google()
    jcenter()
    gradlePluginPortal()
    maven {
      url 'https://jitpack.io'
    }
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
  }

  dependencies {
    classpath deps.plugins.download
    classpath deps.plugins.kotlin
    classpath deps.plugins.dokka
    classpath deps.plugins.intellij
    classpath deps.plugins.android
    classpath deps.plugins.grammarKitComposer
    classpath deps.plugins.publish
    classpath deps.plugins.spotless
    classpath deps.plugins.changelog
    classpath deps.plugins.shadow

    // Used for the sample
//    classpath "com.squareup.sqldelight:gradle-plugin:${versions.sqldelight}"
  }
}

apply plugin: "com.diffplug.spotless"

apply from: "$rootDir/gradle/dependencies.gradle"

spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*", "**/generated/**/*.*", "sqldelight-compiler/integration-tests/src/test/kotlin/com/example/**/*.*"
    ktlint(versions.ktlint).userData(["indent_size": "2"])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events = ["failed", "skipped", "passed"]
      exceptionFormat "full"
    }
  }

  plugins.withId("org.jetbrains.kotlin.jvm") {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  plugins.withId("com.android.library") {
    extensions.getByName("android").compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }
  }

  tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  configurations {
    grammar
  }

  dependencies {
    grammar deps.intellij.indexing
    grammar deps.intellij.analysis
    grammar deps.intellij.asm
  }

  tasks.withType(GenerateParser).configureEach {
    classpath = configurations.grammar + configurations.compileOnly
  }

  configurations.all {
    exclude group: 'com.jetbrains.rd'
    exclude group: 'com.github.jetbrains', module: 'jetCheck'
  }

  group = GROUP
  version = VERSION_NAME
}
