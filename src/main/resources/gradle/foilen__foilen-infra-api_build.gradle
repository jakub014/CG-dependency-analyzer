plugins {
  id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
  mavenLocal()
  mavenCentral()
}

sourceCompatibility = 11

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
  }
}

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

dependencies {

  compile "com.fasterxml.jackson.core:jackson-databind"
  compile "com.foilen:jl-smalltools:$smalltoolsVersion"
  compile "com.google.guava:guava:$guavaVersion"
  compile "javax.xml.bind:jaxb-api"
  compile "org.apache.commons:commons-lang3"
  compile "org.slf4j:slf4j-api"
  
  // Spring
  compile "org.apache.httpcomponents:httpclient"
  compile "org.springframework:spring-context"
  compile "org.springframework:spring-web"
  
  // Test
  testCompileOnly "junit:junit"
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
  testCompile 'org.springframework.data:spring-data-jpa'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask(":uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      pom.project {
        name = "foilen-infra-api"
        packaging 'jar'
        artifactId 'foilen-infra-api'
        description 'This is the infra service API'
        url 'https://github.com/foilen/foilen-infra-api'

        scm {
          connection = "scm:git:git@github.com:foilen/foilen-infra-api.git"
          url = "https://github.com/foilen/foilen-infra-api"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id 'foilen'
            name 'Simon Levesque'
            email 'simon@foilen.com'
          }
        }
      }
    }
  }
}
