plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

ext {
	groupId = "io.github.typelogic"
	artifactId = "mylib2"
	version = "1.2"
}

group project.ext.groupId
archivesBaseName = "mylib2"
version = project.ext.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
	if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
    	//useGpgCmd()
    	sign configurations.archives
	}
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'MyLib2'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'A application used as an example on how to set up pushing  its components to the Central Repository.'
        url 'https://github.com/typelogic/mylib2'

        scm {
          connection 'scm:git:git://github.com/typelogic/mylib2.git'
          developerConnection 'scm:git:ssh://github.com:typelogic/mylib2.git'
          url 'https://github.com/typelogic/mylib2/tree/master'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'typelogic'
            name 'Dexter Aparicio'
            email 'typelogic@pm.me'
          }
        }
      }
    }
  }
}
