plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

group = 'io.github.kennfalcon'

boolean snapshot = "true" == System.getProperty("build.snapshot", "true")
if (snapshot) {
    version = '0.0.1-SNAPSHOT'
} else {
    version = '0.0.1'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenLocal()
    maven {
        url = 'http://repo.maven.apache.org/maven2'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter:2.1.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-configuration-processor:2.1.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-autoconfigure:2.1.1.RELEASE'
    implementation 'org.apache.commons:commons-lang3:3.8'
    implementation 'org.apache.commons:commons-collections4:4.2'
    implementation 'com.alibaba:fastjson:1.2.54'
    implementation 'com.google.guava:guava:28.0-jre'
    compileOnly 'com.aliyun.openservices:tablestore:5.7.0'
    compileOnly 'org.xerial.snappy:snappy-java:1.1.7.7'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = group
            artifactId = 'tablestore-spring-boot-starter'
            version = version
            pom {
                name = 'tablestore-spring-boot-starter'
                description = 'Aliyun TableStore Spring Boot Starter'
                url = 'https://github.com/KennFalcon/tablestore-spring-boot-starter'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Kenn'
                        name = 'Kenn Falcon'
                        email = 'kennfalcon@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/KennFalcon/tablestore-spring-boot-starter.git'
                    developerConnection = 'scm:git:ssh://git@github.com:KennFalcon/tablestore-spring-boot-starter.git'
                    url = 'https://github.com/KennFalcon/tablestore-spring-boot-starter'
                }
            }
        }
    }

    repositories {
        maven {
            name = "MavenCentral"
            if (version.endsWith('-SNAPSHOT')) {
                url = 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
            def USERNAME = System.getProperty("sonatypeUsername")
            def PASSWORD = System.getProperty("sonatypePassword")
            if (USERNAME == null) {
                USERNAME = sonatypeUsername
            }
            if (PASSWORD == null) {
                PASSWORD = sonatypePassword
            }
            credentials {
                username = USERNAME
                password = PASSWORD
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
