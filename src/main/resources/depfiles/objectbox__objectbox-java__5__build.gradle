buildscript {
    ext.javadocDir = file("$buildDir/docs/javadoc")
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// Produce Java 8 byte code, would default to Java 6.
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.named("dokkaHtml") {
    outputDirectory.set(javadocDir)

    dokkaSourceSets {
        configureEach {
            // Fix "Can't find node by signature": have to manually point to dependencies.
            // https://github.com/Kotlin/dokka/wiki/faq#dokka-complains-about-cant-find-node-by-signature-
            externalDocumentationLink {
                // Point to web javadoc for objectbox-java packages.
                url.set(new URL("https://objectbox.io/docfiles/java/current/"))
                // Note: Using JDK 9+ package-list is now called element-list.
                packageListUrl.set(new URL(url, "element-list"))
            }
        }
    }
}

dependencies {
    api project(':objectbox-java')
    api 'io.reactivex.rxjava3:rxjava:3.0.1'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
}

task javadocJar(type: Jar) {
    dependsOn tasks.named("dokkaHtml")
    group = 'build'
    archiveClassifier.set('javadoc')
    from "$javadocDir"
}

task sourcesJar(type: Jar) {
    group = 'build'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

// Set project-specific properties.
publishing.publications {
    mavenJava(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom {
            name = 'ObjectBox RxJava 3 API'
            description = 'RxJava 3 extensions for ObjectBox'
        }
    }
}
