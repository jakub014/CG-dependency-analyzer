plugins {
  id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
  id 'org.springframework.boot' version "$springBootVersion"
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
  mavenLocal()
  mavenCentral()
}

sourceCompatibility = 11

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

dependencies {
  compile "com.foilen:jl-smalltools:$smalltoolsVersion"
  compile "dnsjava:dnsjava:$dnsJavaVersion"
  compile "javax.xml.bind:jaxb-api"
  compile "com.jcraft:jsch:$jschVersion"
  compile "com.foilen:foilen-infra-api:$foilenInfraApiVersion"
  compile "com.foilen:foilen-infra-plugin-core:$foilenInfraPluginVersion"
  compile "com.foilen:foilen-infra-plugins-core:$foilenInfraPluginsCoreVersion"
  compile "org.freemarker:freemarker"
  compile "org.jsoup:jsoup:$jsoupVersion"
  compile "org.springframework.data:spring-data-jpa"
  compile "org.springframework.shell:spring-shell-starter:$springShellVersion"
  
  testCompile "junit:junit"
  testCompile "org.springframework.boot:spring-boot-starter-test"
}

jar {
	enabled = true
}

bootJar {
	classifier = 'boot'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask(":uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      pom.project {
        name = "foilen-infra-cli"
        packaging 'jar'
        artifactId 'foilen-infra-cli'
        description 'A CLI to help managing the infrastructure'
        url 'https://github.com/foilen/foilen-infra-cli'

        scm {
          connection = "scm:git:git@github.com:foilen/foilen-infra-cli.git"
          url = "https://github.com/foilen/foilen-infra-cli"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id 'foilen'
            name 'Simon Levesque'
            email 'simon@foilen.com'
          }
        }
      }
    }
  }
}
