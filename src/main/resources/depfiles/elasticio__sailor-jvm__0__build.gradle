apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.elastic'
version = '3.3.6-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = 1.8
targetCompatibility = 1.8

signing {
    required { isReleaseVersion }
    sign configurations.archives
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compile "org.glassfish:javax.json:1.0.4"
    compile "com.rabbitmq:amqp-client:5.9.0"
    compile "commons-codec:commons-codec:1.10"
    compile "commons-io:commons-io:2.4"
    compile "org.apache.httpcomponents:httpclient:4.5.3"
    compile "com.google.inject:guice:4.1.0"
    compile "com.google.inject.extensions:guice-assistedinject:4.1.0"
    compile "ch.qos.logback:logback-classic:1.2.1"
    compile "ch.qos.logback.contrib:logback-json-classic:0.1.5"
    compile "ch.qos.logback.contrib:logback-jackson:0.1.5"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.5"

    testRuntime 'org.codehaus.groovy:groovy-all:2.0.5'
    testCompile "org.spockframework:spock-core:1.3-groovy-2.4"
    testCompile "com.github.rest-driver:rest-client-driver:2.0.0"
    testCompile "org.eclipse.jetty:jetty-server:9.4.1.v20170120"
    testCompile 'com.github.tomakehurst:wiremock-standalone:2.20.0'
}

uploadArchives {
    repositories {
        mavenLocal()
        if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'elasticio-sailor-jvm'
                    packaging 'jar'
                    description 'Sailor for JVM'
                    url 'http://www.elastic.io'
                    inceptionYear '2015'

                    organization {
                        name 'elastic.io GmbH'
                        url 'http://www.elastic.io'
                    }

                    scm {
                        url 'scm:git@github.com:elasticio/sailor-jvm.git'
                        connection 'scm:git@github.com:elasticio/sailor-jvm.git'
                        developerConnection 'scm:git@github.com:elasticio/sailor-jvm.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    issueManagement {
                        system 'GitHub'
                        url 'https://github.com/elasticio/sailor-jvm/issues'
                    }

                    developers {
                        developer {
                            id 'elasticio'
                            name 'elastic.io GmbH'
                            email 'support@elastic.io'
                        }
                    }
                }
            }
        }
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task javadocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = configurations.compile
}

artifacts {
    archives sourceJar
    archives javadocJar
}

wrapper {
    gradleVersion = '5.4.1'
}

jar {
    manifest {
        attributes("Implementation-Vendor" : "elastic.io Gmbh",
                "Implementation-Title": "elastic.io Sailor form JVM",
                "Implementation-Version": version)
    }
}
