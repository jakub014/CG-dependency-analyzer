plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

group 'io.garam'
version '0.4'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {

    // logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.30'

    // unit test
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.5.10'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'

    // built-in webserver
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.31.v20200723'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.31.v20200723'

    // data bind
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.0'

    // template engine
    compile group: 'com.samskivert', name: 'jmustache', version: '1.15'
}

test {
    useJUnitPlatform()
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

bintray {
    user = project.property('bintray_usr')
    key = project.property('bintray_key')
    publications = ['GaramPublication']

    pkg {
        repo = 'garam-framework'
        name = 'garam'
        userOrg = 'hyeyoom'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/garamio/garam.git'
        version {
            name = '0.4'
            released  = new Date()
            vcsTag = '0.4'
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
        }
    }
    developers {
        developer {
            id "hyeyoom"
            name "Chiho Won"
            email "high.neoul@gmail.com"
            organization "Github"
            organizationUrl "https://github.com"
        }
    }
    scm {
        connection "scm:git:git://github.com/garamio/garam.git"
        developerConnection "scm:git:ssh://github.com:garamio/garam.git"
        url "https://github.com/garamio/garam.git"
    }
}

publishing {
    publications {
        GaramPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'io.garam'
            artifactId 'core'
            version '0.4'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Lightweight web framework')
                root.appendNode('name', 'core')
                root.appendNode('url', 'https://github.com/garamio/garam')
                root.appendNode('packaging', 'jar')
                root.children().last() + pomConfig
            }
        }
    }
}