repositories {
  mavenCentral()
}


wrapper {
    gradleVersion = '5.6.4'
}

allprojects {
  version = '0.19.0'
  group = 'com.nedap.healthcare.archie'
  ext.gradleScriptDir = "${rootProject.projectDir}/gradle"
  //archivesBaseName = 'archie'

  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:all', '-quiet')
  }
}

gradle.ext.ossrhUsernameSafe = hasProperty('ossrhUsername') ? ossrhUsername : ""
gradle.ext.ossrhPasswordSafe = hasProperty('ossrhPassword') ? ossrhPassword : ""
gradle.ext.shouldSign = hasProperty('signing.keyId')
gradle.ext.isParallel = hasProperty('org.gradle.parallel') ? project.property('org.gradle.parallel') == 'true' : false

subprojects {
  apply plugin: 'java'
  apply from: "${gradleScriptDir}/publish-maven.gradle"

 // plugins {
   //   id 'com.github.ben-manes.versions' version '0.13.0'
 // }

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  compileJava.options.encoding = "UTF-8"
  compileTestJava.options.encoding = "UTF-8"

  repositories {
      mavenCentral()
      maven { url 'https://jitpack.io' }
  }

  ext.reflectionsVersion = '0.9.12'
  ext.jacksonVersion = '2.11.3'


  dependencies {
    compile 'org.slf4j:slf4j-api:1.7.30'

    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

    compile 'com.google.guava:guava:30.1.1-jre'

    compile "org.reflections:reflections:${reflectionsVersion}"
    compile 'com.esotericsoftware.kryo:kryo5:5.1.0'

    compile('commons-io:commons-io:2.8.0'){
      exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile 'org.apache.commons:commons-text:1.9'
    compile 'org.apache.commons:commons-lang3:3.12.0'

    //java 10 no longer has these included by default, so explicit dependency is needed.
    compile 'javax.xml.bind:jaxb-api:2.3.1'
    compile 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    runtime 'com.sun.xml.bind:jaxb-impl:2.3.3'
    compile 'javax.activation:activation:1.1.1'

    implementation 'org.threeten:threeten-extra:1.6.0'

    testCompile 'junit:junit:4.+'
    testCompile 'org.slf4j:slf4j-simple:1.7.30'

    compile 'com.github.zafarkhaja:java-semver:0.9.0'


  }


  javadoc {
      options.encoding = 'UTF-8'
  }

  //configure the jar files: sources and javaod
  task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
  }

  task sourcesJar(type: Jar) {
      classifier = 'sources'
      from sourceSets.main.allSource
  }

  artifacts {
      archives javadocJar, sourcesJar
  }

  test {
   testLogging {
      events "failed"
      exceptionFormat "full"
    }
  }

}

apply from: "${gradleScriptDir}/jacoco.gradle"
