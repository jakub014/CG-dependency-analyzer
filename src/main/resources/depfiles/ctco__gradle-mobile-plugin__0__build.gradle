/*
 * @(#)build.gradle
 *
 * Copyright C.T.Co Ltd, 15/25 Jurkalnes Street, Riga LV-1046, Latvia. All rights reserved.
 */

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.22.0'
    id 'net.researchgate.release' version '2.8.1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jparsec', name: 'jparsec', version: '2.2.1'
    implementation group: 'com.googlecode.plist', name: 'dd-plist', version: '1.23'
    implementation group: 'com.eclipsesource.minimal-json', name: 'minimal-json', version: '0.9.5'

    implementation group: 'commons-io', name: 'commons-io', version:'2.8.0'
    implementation group: 'org.apache.commons', name:'commons-compress', version:'1.20'
    implementation group: 'org.apache.commons', name:'commons-exec', version:'1.3'
    implementation group: 'org.apache.commons', name:'commons-lang3', version:'3.11'

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'

    compile gradleApi()
    compile localGroovy()

    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}

group = 'lv.ctco.scm'
archivesBaseName = 'gradle-mobile-plugin'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnit()
}

jar {
    manifest {
        attributes ("Specification-Vendor": "C.T.Co, Ltd.",
                    "Specification-Title": "Gradle Mobile Plugin",
                    "Specification-Version": project.getVersion().minus('-SNAPSHOT'),
                    "Implementation-Vendor": "C.T.Co, Ltd.",
                    "Implementation-Title": "gradle-mobile-plugin",
                    "Implementation-Version": project.getVersion())
    }
}

// Custom functionality for TeamCity integration
import lv.ctco.scm.teamcity.ProjectInfoTask
tasks.register('projectInfo', ProjectInfoTask)

// Custom functionality for Wercker integration
import lv.ctco.scm.wercker.SetupConfigFileTask
tasks.register('werckerSetupGradleProperties', SetupConfigFileTask) {
    envVar = 'GRADLE_PROPERTIES'
    filePath = '~/.gradle/gradle.properties'
}

signing {
    required { hasProperty("signingKey") && hasProperty("signingPassword") && gradle.taskGraph.hasTask("uploadArchives") }
    useInMemoryPgpKeys(findProperty("signingKey").toString(), findProperty("signingPassword").toString())
    sign configurations.archives
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: findProperty("ossrhUsername"), password: findProperty("ossrhPassword"))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: findProperty("ossrhUsername"), password: findProperty("ossrhPassword"))
            }

            pom.project {
                name 'C.T.Co Mobile Plugin for Gradle'
                packaging 'jar'
                description 'The C.T.Co Mobile Plugin for Gradle helps you to configure and build Xcode and Xamarin (iOS, Android) apps.'
                url 'https://github.com/ctco/gradle-mobile-plugin'

                scm {
                    connection 'scm:git:git@github.com:ctco/gradle-mobile-plugin.git'
                    developerConnection 'scm:git:git@github.com:ctco/gradle-mobile-plugin.git'
                    url 'https://github.com/ctco/gradle-mobile-plugin'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'Quesar'
                        name 'Ivars Bērziņš'
                        email 'Ivars.Berzinsh@ctco.lv'
                    }
                }
            }
        }
    }
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = false
    failOnUpdateNeeded = true
    revertOnFail = true
    preTagCommitMessage = '[Release] [skip ci] - pre tag commit: '
    newVersionCommitMessage = '[Release] [skip ci] - new version commit: '
    tagTemplate = '${version}'
    versionPropertyFile = 'gradle.properties'
    versionProperties = []
    buildTasks = ['build']
}

nexusStaging {
    packageGroup = 'lv.ctco'
    numberOfRetries = 60
    delayBetweenRetriesInMillis = 5000
}

afterReleaseBuild.dependsOn uploadArchives
