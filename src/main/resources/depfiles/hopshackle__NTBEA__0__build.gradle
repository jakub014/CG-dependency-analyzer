plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'maven'
    id 'signing'
}

group = "com.github.hopshackle"
archivesBaseName = "NTBEA"
version = "0.4-SNAPSHOT"

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs = ['src']
    main.kotlin.srcDirs = ['src']
}
    dependencies {
        compile files('./lib/jgpml.jar')
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.10"
        implementation "org.apache.commons:commons-math3:3.6.1"
        implementation "gov.nist.math:jama:1.0.3"
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task functionJar(type: Jar) {
    from sourceSets.main.output
    manifest {
        attributes "Main-Class": "mathFunctions.FunctionReportKt"
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    archiveFileName = "FunctionNTBEA.jar"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.sonatype_user, password: project.sonatype_password)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.sonatype_user, password: project.sonatype_password)
            }

            pom.project {
                name 'NTBEA'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Parameter optimisation with the N-Tuple Bandit Evolutionary Algorithm'
                url 'http://www.github.com/hopshackle/NTBEA'

                scm {
                    connection 'scm:git:git://github.com/hopshackle/NTBEA.git'
                    developerConnection 'scm:git:git://github.com/hopshackle/NTBEA.git'
                    url 'http://github.com/hopshackle/NTBEA'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'hopshackle'
                        name 'James Goodman'
                        email 'james@janigo.co.uk'
                    }
                }
            }
        }
    }
}