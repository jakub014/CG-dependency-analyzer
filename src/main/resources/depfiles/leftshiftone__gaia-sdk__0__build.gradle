import groovy.json.JsonBuilder
import groovy.json.JsonSlurper
import org.gradle.api.artifacts.DependencyResolveDetails
import org.gradle.api.tasks.bundling.Jar

import java.util.regex.Matcher
import java.util.regex.Pattern

buildscript {
    ext {
        kotlinVersion = "1.3.41"
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "one.leftshift.asteria:asteria-code-analytics:latest.release"
        classpath "one.leftshift.asteria:asteria-dependency:latest.release"
        classpath "one.leftshift.asteria:asteria-docs:latest.release"
        classpath "one.leftshift.asteria:asteria-email:latest.release"
        classpath "one.leftshift.asteria:asteria-publish:latest.release"
        classpath "one.leftshift.asteria:asteria-report:latest.release"
        classpath "one.leftshift.asteria:asteria-version:latest.release"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
    }
}

apply plugin: "antlr"
apply plugin: "kotlin"
apply plugin: "one.leftshift.asteria-email"
apply plugin: "one.leftshift.asteria-report"
apply plugin: "one.leftshift.asteria-publish"

apply plugin: "org.jetbrains.dokka"
apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: "io.codearte.nexus-staging"

nexusStaging {
    packageGroup = "one.leftshift"
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    stagingProfileId = System.getenv("OSSRH_STAGING_PROFILE_ID")
    username = System.getenv("OSSRH_LOGIN_NAME")
    password = System.getenv("OSSRH_LOGIN_PASSWORD")
}

asteriaEmail {
    smtpHost = System.getenv("ASTERIA_EMAIL_SMTP_HOST")
    if (System.getenv("ASTERIA_EMAIL_SMTP_PORT")) {
        smtpPort = System.getenv("ASTERIA_EMAIL_SMTP_PORT").toInteger()
    }
    smptUser = System.getenv("ASTERIA_EMAIL_SMTP_USER")
    smptPassword = System.getenv("ASTERIA_EMAIL_SMTP_PASSWORD")
    sender = System.getenv("ASTERIA_EMAIL_SENDER")
}

asteriaReport {
    reportingUrl = System.getenv("ASTERIA_REPORT_URL")
    depsJsonResult = file("${rootProject.buildDir}/dependencyUpdates/report.json")
    junitXmlResults = fileTree(rootProject.projectDir) {
        include(
                "**/test-results/*/TEST-*.xml",
                "**/gaia-sdk-javascript/build/test/TEST-*.xml",
                "**/gaia-sdk-python/build/test/TEST-*.xml"
        )
    }
    junitBinaryResults = files(subprojects.collect { subproject ->
        ["test"].collect {
            "${subproject.buildDir}/test-results/${it}/binary"
        }
    }.flatten())
}

asteriaPublish {
    enableBranchSnapshotRepositories = true
}

allprojects {
    apply plugin: "one.leftshift.asteria-dependency"
    apply plugin: "one.leftshift.asteria-version"

    repositories {
        jcenter()
    }

    asteriaDependency {
        enableBranchSnapshotRepositories = true
    }
    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.13.RELEASE") {
                bomProperty("kotlin.version", kotlinVersion)
            }
        }
        dependencies {
            dependency "org.apache.velocity:velocity-engine-core:2.1"
        }
    }
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == "org.jetbrains.kotlin") {
                details.useVersion kotlinVersion
            }
        }
    }

    if (System.getenv("CI")) {
        afterEvaluate { rootProject.tasks.postRelease.finalizedBy rootProject.tasks.sendReleaseEmail }
        if (tasks.findByName("publish")) {
            afterEvaluate { rootProject.tasks.postRelease.dependsOn tasks.publish }
        }
    }
}

repositories {
    jcenter()
    maven {
        credentials(AwsCredentials) {
            accessKey gradle.awsAccessKey
            secretKey gradle.awsSecretKey
        }
        url gradle.releasesRepositoryUrl
    }
    maven {
        credentials(AwsCredentials) {
            accessKey gradle.awsAccessKey
            secretKey gradle.awsSecretKey
        }
        url gradle.snapshotsRepositoryUrl
    }
}


dependencies {
    antlr "org.antlr:antlr4:4.7.2" // use ANTLR version 4

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.apache.velocity:velocity-engine-core"

    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core"
}

test {
    useJUnitPlatform()
}

sourceSets.main.java.srcDirs += new File(buildDir, "generated-src/antlr")
generateGrammarSource {
    outputDirectory = new File(project.rootDir, "build/generated-src/antlr/gaia/sdk/antlr")
    arguments += ["-visitor", "-package", "gaia.sdk.antlr"]
}
compileKotlin.dependsOn generateGrammarSource

task generateSource(type: Test) {
    useJUnitPlatform()
    filter {
        includeTestsMatching "gaia.sdk.codegen.KotlinCodegenTest"
    }
}
generateSource.dependsOn generateGrammarSource


subprojects {
    apply plugin: "one.leftshift.asteria-code-analytics"

    asteriaCodeAnalytics {
        sonarUrl = System.getenv("SONAR_HOST_URL")
        sonarLoginToken = System.getenv("SONAR_LOGIN")
        xmlCoverageReportEnabled = true
        sonarProperties = [
                "sonar.test.inclusions"            : "**/*Test.*,**/*.test.ts,**/tests/test_*.py",
                "sonar.javascript.lcov.reportPaths": "${project(":gaia-sdk-javascript").projectDir}/coverage/lcov.info",
                "sonar.typescript.lcov.reportPaths": "${project(":gaia-sdk-javascript").projectDir}/coverage/lcov.info",
                "sonar.python.coverage.reportPath" : "${project(":gaia-sdk-python").projectDir}/build/coverage/coverage.xml",
        ]
        coverageExcludes = ["**/*Kt.class"]
    }
}

task setVersionJavascript() {
    doLast {
        String packageJsonPath = project(":gaia-sdk-javascript").projectDir.toString() + "/package.json"
        File packageJsonFile = file(packageJsonPath)
        def parsedJson = new JsonSlurper().parseText(packageJsonFile.text)
        parsedJson.version = version.toString()
        packageJsonFile.text = new JsonBuilder(parsedJson).toPrettyString()
        logger.quiet("Updated version in ${packageJsonPath} to ${version.toString()}")
    }
}
task setVersionPython() {
    doLast {
        String pyprojectTomlPath = project(":gaia-sdk-python").projectDir.toString() + "/pyproject.toml"
        File pyprojectTomlFile = file(pyprojectTomlPath)
        String pyprojectTomlContent = pyprojectTomlFile.text

        Pattern regex = Pattern.compile("^version\\s?=\\s?[\"'].*[\"']\\s?\$", Pattern.MULTILINE)
        Matcher matcher = regex.matcher(pyprojectTomlContent)
        List<String> matches = []
        while (matcher.find()) {
            matches.add(matcher.group(0))
        }
        if (matches.size() > 1)
            throw new RuntimeException("More than one version found in ${pyprojectTomlPath}")
        if (matches.isEmpty() || matches.first().trim().isEmpty())
            throw new RuntimeException("No version found in ${pyprojectTomlPath}")
        pyprojectTomlFile.text = pyprojectTomlContent.replaceAll(matches.first(), "version = \"${version.toString()}\"")
        logger.quiet("Updated version in ${pyprojectTomlPath} to ${version.toString()}")
    }
}
task commitVersions() {
    doLast {
        // javascript
        String packageJsonPath = "${project(":gaia-sdk-javascript").projectDir}/package.json"
        executeAssertExitCode(["git", "add", packageJsonPath])
        // python
        String pyprojectTomlPath = "${project(":gaia-sdk-python").projectDir}/pyproject.toml"
        executeAssertExitCode(["git", "add", pyprojectTomlPath])

        String commitMessage = "set version to ${version.toString()}"
        executeWarnExitCode(["git", "commit", "-m", "'$commitMessage'".toString()])
    }
}
commitVersions.dependsOn setVersionJavascript, setVersionPython

task pushVersions() {
    doLast {
        executeAssertExitCode(["git", "push"])
    }
}
pushVersions.dependsOn commitVersions
if (System.getenv("CI") == null || System.getenv("CI") != "true") {
    afterEvaluate { rootProject.tasks.release.dependsOn rootProject.tasks.pushVersions }
}

private void executeAssertExitCode(List<String> command) {
    int exitCode = execute(command)
    if (exitCode != 0) throw new RuntimeException("Command '${command.join(" ")}' failed with code ${exitCode}")
    logger.debug("Executed command '${command.join(" ")}'")
}

private void executeWarnExitCode(List<String> command) {
    int exitCode = execute(command)
    if (exitCode != 0) logger.warn("Command failed with code ${exitCode}")
    logger.debug("Executed command '${command.join(" ")}'")
}

private int execute(List<String> command) {
    logger.debug("Executing command '${command.join(" ")}'")
    def process = new ProcessBuilder(command).directory(projectDir).start()
    process.consumeProcessOutput(System.out, System.err)
    process.waitFor()
    logger.debug("Executed command '${command.join(" ")}'")
    return process.exitValue()
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "${buildDir}/dokkaJavadoc"
}
task javadocJar(type: Jar) {
    from dokkaJavadoc
    classifier "javadoc"
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}
artifacts {
    archives javadocJar, sourcesJar
}
publishing {
    publications {
        release(MavenPublication) {
            artifactId = project.name
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                Node root = asNode()
                root.appendNode("name", "GAIA SDK")
                root.appendNode("description", "GAIA SDK is an open source software library in order to interact with the GAIA API.")
                root.appendNode("url", "https://github.com/leftshiftone/gaia-sdk")

                Node licenses = root.appendNode("licenses")
                Node license = licenses.appendNode("license")
                license.appendNode("name", "MIT")
                license.appendNode("url", "https://github.com/leftshiftone/gaia-sdk/blob/master/LICENSE")

                Node developers = root.appendNode("developers")
                Node developer = developers.appendNode("developer")
                developer.appendNode("id", "leftshiftone")
                developer.appendNode("name", "Leftshift One")
                developer.appendNode("email", "contact@leftshift.one")
                developer.appendNode("organization", "Leftshift One Software GmbH")
                developer.appendNode("organizationUrl", "https://www.leftshift.one")

                Node scm = root.appendNode("scm")
                scm.appendNode("connection", "scm:git:git://github.com/leftshiftone/gaia-sdk.git")
                scm.appendNode("developerConnection", "scm:git:ssh://github.com:leftshiftone/gaia-sdk.git")
                scm.appendNode("url", "https://github.com/leftshiftone/gaia-sdk")
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = version.toString().endsWith("SNAPSHOT") ? "https://s01.oss.sonatype.org/content/repositories/snapshots" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username System.getenv("OSSRH_LOGIN_NAME")
                password System.getenv("OSSRH_LOGIN_PASSWORD")
            }
        }
    }
}

if (System.getenv("CI") == null || System.getenv("CI") != "true") {
    tasks.release.dependsOn tasks.generateLock
    tasks.release.dependsOn tasks.saveLock
    tasks.release.dependsOn tasks.persistDependencyLock
}
