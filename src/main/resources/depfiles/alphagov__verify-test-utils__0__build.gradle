plugins { 
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.github.gradle-nexus.publish-plugin'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

def buildVersion = "2.0.0-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
group = "uk.gov.ida"
version = "$buildVersion"
archivesBaseName = "common-test-utils"

repositories {
    maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
}

ext {
    dropwizard_version = "1.3.17"
}

dependencies {
    implementation  'org.junit.jupiter:junit-jupiter-api:5.5.2',
                    'org.junit.jupiter:junit-jupiter-migrationsupport:5.5.2',
                    'com.google.guava:guava:28.1-jre',
                    'org.eclipse.jetty:jetty-server:9.0.7.v20131107',
                    'commons-lang:commons-lang:2.6',
                    'commons-io:commons-io:2.4',
                    "io.dropwizard:dropwizard-jackson:$dropwizard_version"

    api  "io.dropwizard:dropwizard-client:$dropwizard_version"

    testImplementation  'org.assertj:assertj-core:3.14.0',
                        'org.mockito:mockito-core:3.2.0'
}

nexusPublishing {
    useStaging = true
    repositories {
        sonatype {
            // because we registered in Sonatype after 24 Feb 2021, we provide these URIs
            // see: https://github.com/gradle-nexus/publish-plugin/blob/master/README.md
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("SONATYPE_USERNAME")
            password = System.getenv("SONATYPE_PASSWORD")
            System.out.println("Configured nexusPublishing.repositories.sonatype")
        }
    }
}

signing {
    useInMemoryPgpKeys(
        System.getenv("MAVEN_CENTRAL_SIGNING_KEY"),
        System.getenv("MAVEN_CENTRAL_SIGNING_KEY_PASSWORD")
    )
    sign publishing.publications
    System.out.println("Signed configurations.archives")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'SAML Test Utils'
                packaging = 'jar'
                description = 'Library for various SAML testing utility operations.'
                url = 'https://github.com/alphagov/verify-test-utils'
                artifactId = 'common-test-utils'

                scm {
                    url = 'https://github.com/alphagov/verify-test-utils'
                    connection = 'scm:git:git://github.com/alphagov/verify-test-utils.git'
                    developerConnection = 'scm:git:ssh://git@github.com:alphagov/verify-test-utils.git'
                }

                licenses {
                    license {
                        name = 'MIT Licence'
                        url = 'https://github.com/alphagov/verify-test-utils/blob/master/LICENCE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'GDS Developers'
                    }
                }
            } // pom
            System.out.println("Configured pom")
        }
    }
}

task sonar(dependsOn: [':clean', ':build', 'sonarRunner'])

task buildJars(dependsOn: ['jar', 'sourceJar', 'javadocJar'])
