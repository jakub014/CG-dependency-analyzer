plugins {
    id 'java'
    id 'signing'
    id 'maven'
}

group 'com.github.lmm1990'
version '1.2'

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'
    testCompile group: 'junit', name: 'junit', version: '4.13'
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}
javadoc {
    options.encoding = 'UTF-8'
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
if(!System.getProperty("os.name").toLowerCase().startsWith("win")){
    project.ext {
        ossrhUsername = "userName"
        ossrhPassword = "pwd"
    }
}
allprojects {
    // Signature of artifacts
    signing {
        sign configurations.archives
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    // OSSRH publication
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'jtemplate'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'javaç‰ˆtemplate'
                    url 'https://github.com/lmm1990/jtemplate'

                    scm {
                        connection 'scm:git:git@github.com:lmm1990/jtemplate.git'
                        developerConnection 'scm:git:git@github.com:lmm1990/jtemplate.git'
                        url 'https://github.com/lmm1990/jtemplate'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'liumingming'
                            name 'liumingming'
                            email '544484078@qq.com'
                        }
                    }
                }
            }
        }
    }
}