plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'maven'
	id 'signing'
}

group = "com.github.paopaoyue"
archivesBaseName = "ypp-jwt"
version = "1.0.2"
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation group: 'com.auth0', name: 'java-jwt', version: '3.10.3'
	implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'ypp-jwt'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'A simple JWT authorization framework for Spring Boot applications.'
        url 'https://github.com/PaoPaoYue/ypp-jwt'

        scm {
          connection 'scm:git:git@github.com:PaoPaoYue/ypp-jwt.git'
          developerConnection 'scm:git:git@github.com:PaoPaoYue/ypp-jwt.git'
          url 'git@github.com:PaoPaoYue/ypp-jwt.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
          }
        }

        developers {
          developer {
            id 'ypp'
            name 'Yue Peng'
            email 'paopaoyue.cn@gmail.com'
          }
        }
      }
    }
  }
}
