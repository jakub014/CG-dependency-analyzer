/*
 * Copyright 2019 Apple Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
 *
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */
ext.sourceCompatibility = JavaVersion.VERSION_1_8
ext.targetCompatibility = JavaVersion.VERSION_1_8


ext {
    androidPluginVersion = "4.1.0"
    ddmlibVersion = "26.5.0"
    jcommanderVersion = "1.48"
    slf4jVersion = "1.7.21"
    guavaVersion = "19.0"
    gsonVersion = "2.8.5"
    jsr305Version = "3.0.2"
    commonsIoVersion = "2.5"
    commonsLangVersion = "3.4"
    mustacheVersion = "0.8.0"
    junitVersion = "4.12"
    jmockVersion = "2.8.2"
    hamcrestVersion = "1.3"
    shazamcrestVersion = "0.11"
}

def repos = {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'https://maven.google.com'
    }
}

allprojects {
    buildscript {
        repositories repos
    }
    repositories repos

    apply plugin: "java"
    apply plugin: "jacoco"

    version = VERSION_NAME
    group = GROUP
}

subprojects {
    afterEvaluate { project ->
        jacocoTestReport.dependsOn test
        jacocoTestReport.reports.html.enabled false
        jacocoTestReport.reports.xml.enabled true

        // Weirdly archives are signed even if signing.required {false}, so also skipping task explicitly.
        signArchives.onlyIf { !VERSION_NAME.endsWith("SNAPSHOT") }
    }
}

task jacocoMergeExecutions(type: JacocoMerge, dependsOn: getTasksByName('jacocoTestReport', true)) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
}

task jacocoMergedReport(type: JacocoReport, dependsOn: jacocoMergeExecutions) {
    executionData jacocoMergeExecutions.destinationFile
    sourceDirectories.from(files(subprojects.sourceSets.main.allSource.srcDirs))
    classDirectories.from(files(subprojects.sourceSets.main.output))
    reports {
        html.enabled false
        xml.enabled true
    }
}
