/**
 * This build.gradle exists to publish our capsule (executable fat jar) to maven. It cannot be placed in the
 * webserver project because the bintray plugin cannot publish two modules from one project.
 */
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'us.kirchmeier.capsule'
apply plugin: 'com.jfrog.artifactory'

description 'Corda node web server capsule'

configurations {
    runtimeArtifacts
    capsuleRuntime
}

dependencies {
    // TypeSafe Config: for simple and human friendly config files.
    capsuleRuntime "com.typesafe:config:$typesafe_config_version"
}

jar.enabled = false

capsule {
    version capsule_version
}

task buildWebserverJar(type: FatCapsule, dependsOn: project(':node').tasks.jar) {
    applicationClass 'net.corda.webserver.WebServer'
    archiveBaseName = 'corda-testserver'
    archiveVersion = corda_release_version
    archiveClassifier = jdkClassifier
    archiveName = archiveFileName.get()
    applicationSource = files(
        project(':testing:testserver').configurations.runtimeClasspath,
        project(':testing:testserver').tasks.jar,
        project(':testing:testserver').sourceSets.main.java.outputDir.toString() + '/CordaWebserverCaplet.class',
        project(':testing:testserver').sourceSets.main.java.outputDir.toString() + '/CordaWebserverCaplet$1.class',
        project(':node').buildDir.toString() + '/resources/main/corda-reference.conf',
        "$rootDir/config/dev/log4j2.xml",
        project(':node:capsule').projectDir.toString() + '/NOTICE' // Copy CDDL notice
    )
    from configurations.capsuleRuntime.files.collect { zipTree(it) }

    capsuleManifest {
        applicationVersion = corda_release_version
        javaAgents = quasar_classifier ? ["quasar-core-${quasar_version}-${quasar_classifier}.jar"] : ["quasar-core-${quasar_version}.jar"]
        systemProperties['visualvm.display.name'] = 'Corda Webserver'
        minJavaVersion = '1.8.0'
        minUpdateVersion['1.8'] = java8_minUpdateVersion
        caplets = ['CordaWebserverCaplet']

        // JVM configuration:
        // - Constrain to small heap sizes to ease development on low end devices.
        // - Switch to the G1 GC which is going to be the default in Java 9 and gives low pause times/string dedup.
        //
        // If you change these flags, please also update Driver.kt
        jvmArgs = ['-Xmx200m', '-XX:+UseG1GC']
    }

    manifest {
        if (JavaVersion.current() == JavaVersion.VERSION_11) {
            attributes('Add-Opens': 'java.management/com.sun.jmx.mbeanserver java.base/java.lang')
        }
    }
}

artifacts {
    archives buildWebserverJar
    runtimeArtifacts buildWebserverJar
    publish buildWebserverJar {
        classifier ''
    }
}

publish {
    disableDefaultJar = true
    name 'corda-testserver'
}
