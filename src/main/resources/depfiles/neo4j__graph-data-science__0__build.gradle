plugins {
    id 'com.github.hierynomus.license'              version '0.15.0'                apply false
    id 'com.github.johnrengelman.shadow'            version '5.2.0'                 apply false
    id 'de.undercouch.download'                     version '3.4.3'                 apply false
    id 'ch.kk7.spawn'                               version '1.0.20180924200750'    apply false
    id 'com.vanniktech.dependency.graph.generator'  version '0.5.0'
    id 'de.thetaphi.forbiddenapis'                  version '2.7'                   apply false
    id 'com.github.spotbugs'                        version '4.4.4'                 apply false
    id 'nebula.info-dependencies'                   version '9.1.1'                 apply false
    id 'com.google.protobuf'                        version '0.8.14'                apply false
}

ext {
    distributionDir = "$buildDir/distributions"
    cliDistributionDir = "$buildDir/distributions/estimation-cli"
    distributionLicenseFile = file('LICENSE.txt')
    publicDir = "${project.rootDir}"

    checkstyleReportsDir = "${buildDir}/reports/checkstyle/"

    spotbugsToolVersion = '4.0.6'
    spotbugsReportsDir = "${buildDir}/reports/spotbugs/"
    SpotBugsTask = com.github.spotbugs.snom.SpotBugsTask

    compatibilityModules = [
            'kernel-adapter': [
                    project(':neo4j-kernel-adapter-4.0'),
                    project(':neo4j-kernel-adapter-4.1'),
                    project(':neo4j-kernel-adapter-4.2'),
            ]
    ]
}

apply from: 'gradle/version.gradle'

allprojects {
    apply plugin: 'base'

    group = gdsGroup
    version = gdsVersion

    task ci {
        dependsOn tasks.check
    }

    task cipr {
        dependsOn tasks.check
    }

    task checkstyle {
        group = "verification"
        description = "Run all checkstyle related tasks"
    }

    task spotbugs {
        group = "verification"
        description = "Run all spotbugs related tasks"
    }
}

apply from: 'gradle/licensing.gradle'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/defaults-java.gradle'
apply from: 'gradle/forbidden-apis.gradle'
apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/spotbugs.gradle'
apply from: 'gradle/pregel-bootstrap.gradle'
