/*
 * Copyright (c) 2016 Vikash Madhow
 */

/**
 * Long-term serialization library.
 *
 * @author Vikash Madhow (vikash.madhow@gmail.com)
 */
buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }
}

plugins {
  id "java-library"
  id "maven-publish"
  id "signing"
  id 'io.codearte.nexus-staging' version '0.22.0'
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java']
    }
    resources {
      srcDirs = ['src/main/resources']
    }
  }
  test {
    java {
      srcDirs = ['src/test/java']
    }
    resources {
      srcDirs = ['src/test/resources']
    }
  }
}

dependencies {
  compile "com.vikmad:base:0.1.2"
  compile "org.yaml:snakeyaml:latest.release"

  testImplementation "junit:junit:latest.release"
}

group = "com.vikmad"
archivesBaseName = 'serializer'
sourceCompatibility = JavaVersion.VERSION_14
version = "0.4.2"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  repositories {
    maven {
      def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
      url = isReleaseVersion ? releaseRepo : snapshotRepo
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        groupId = 'com.vikmad'
        artifactId = 'serializer'
        name = "Long term serialization"
        description = 'A library for serializing (and deserializing) Java object graphs to various text formats such as XML, JSON and YAML, useful for long-term storage with a focus on human readability and external modification'
        url = 'https://github.com/vikashmadhow/long-term-serialization'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = "vikashmadhow"
            name = "Vikash Madhow"
            email = "vikash.madhow@gmail.com"
            url = "http://vikmad.com"
          }
        }
        scm {
          url = 'https://github.com/vikashmadhow/long-term-serialization'
          connection = 'scm:https://github.com/vikashmadhow/long-term-serialization.git'
          developerConnection = 'scm:git://github.com/vikashmadhow/long-term-serialization.git'
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}

nexusStaging {
  packageGroup = group
  username = project.getProperty('ossrhUsername')
  password = project.getProperty('ossrhPassword')
}
