apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.tananaev'
version = '1.3'

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'adblib'
                    packaging 'jar'
                    description 'A Java library implementation of the ADB (Android Debug Bridge) network protocol.'
                    url 'https://github.com/tananaev/adblib'

                    scm {
                        connection 'scm:git:https://github.com/tananaev/adblib'
                        developerConnection 'scm:git:git@github.com:tananaev/adblib.git'
                        url 'https://github.com/tananaev/adblib'
                    }

                    licenses {
                        license {
                            name 'BSD 3-Clause License'
                            url 'http://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            id 'tananaev'
                            name 'Anton Tananaev'
                            email 'anton.tananaev@gmail.com'
                        }
                    }
                }
            }
        }
    }

}
