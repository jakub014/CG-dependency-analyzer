buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.+')
    }
}

plugins {
    id "com.github.spotbugs" version "4.6.0"
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'jacoco'
}

apply plugin: 'maven'
apply plugin: 'com.jfrog.artifactory'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}


task setVersion {
    if (System.env.BUILD_NUMBER) {
        version = projectVersionMajor + '.' + projectVersionMinor + '.' + System.env.BUILD_NUMBER
    } else {
        version = projectVersionMajor + '.' + projectVersionMinor + '.' + projectVersionBuild
    }
}

jar {
    baseName = projectName
    manifest
            {
                attributes 'Implementation-Title': projectName,
                        'Implementation-Version': version
            }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task createPom {
    doLast {
        pom
        {
            project {
                artifactId 'jackson-json-crypto'
                groupId 'com.codingrodent'
                name 'com.codingrodent.jackson.crypto'
                description 'A Crypto Module for Jackson'
                url 'https://github.com/codesqueak/jackson-json-crypto'
                scm {
                    url 'https://github.com/codesqueak/jackson-json-crypto'
                    connection 'scm:git:git://github.com/codesqueak/jackson-json-crypto.git'
                    developerConnection 'scm:git:ssh://github.com:codesqueak/jackson-json-crypto.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'codesqueak'
                        name 'codesqueak'
                        email 'codesqueak@gmail.com'
                        organizationUrl 'http://www.codesqueak.com'
                        //  organization 'codesqueak'
                    }
                }
            }
        }.writeTo("$buildDir/libs/" + projectName + "-" + version + ".pom")
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.6"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

test {
    useJUnitPlatform()
    maxParallelForks = 1
    filter
            {
                includeTestsMatching "com.codingrodent.jackson.*"
            }
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination = file("${buildDir}/jacocoHtml")
    }
}

spotbugs {
    toolVersion = '4.2.0'
    ignoreFailures = false
    effort = 'max'

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'gradle-dev-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}


dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.12.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.12.0'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'org.hibernate', name: 'hibernate-validator', version: '6.1.7.Final'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    implementation group: 'org.glassfish', name: 'javax.el', version: '3.0.0'
    implementation group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
    //
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'
    testCompile group: 'org.easymock', name: 'easymock', version: '4.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
}

wrapper {
    gradleVersion = '6.7.1'
    distributionType = Wrapper.DistributionType.BIN
}

