buildscript {
    ext {
        kotlinVersion = '1.4.10'
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
}

apply plugin: 'kotlin'

group = "com.netsensia.rivalchess"
version = '28.0.0'
jar.baseName("rivalchess-vie-utils")

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "$group"
            artifactId = "${jar.baseName}"
            version = '28.0.0'
            from components.java
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name "$jar.baseName"
                packaging 'jar'
                description 'Utility classes for RivalChess Vie services'
                url "https://github.com/chris-moreton/$jar.baseName"

                scm {
                    connection = "scm:git:git://github.com/chris-moreton/${jar.baseName}.git"
                    developerConnection = "scm:git:ssh://github.com/chris-moreton/${jar.baseName}.git"
                    url = "https://github.com/chris-moreton/$jar.baseName"
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'chris-moreton'
                        name = 'Chris Moreton'
                        email = 'chris@netsensia.com'
                    }
                }
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
    
    compile 'org.apache.activemq:activemq-core:5.4.3'

    implementation 'com.amazonaws:aws-java-sdk-s3:1.11.7'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.3"
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.netsensia.rivalchess:rivalchess-vie-model:5.0.0'
}

test {
    useJUnit()
    maxHeapSize = '1G'
    testLogging.showStandardStreams = true
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
