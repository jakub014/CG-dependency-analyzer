group 'br.com.simpli'
version '3.3.0'

apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'signing'

buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.9"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'javax', name: 'javaee-web-api', version: '7.0'
    implementation group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4'
    implementation group: 'commons-pool', name: 'commons-pool', version: '1.6'
    implementation group: 'commons-collections', name: 'commons-collections', version: '3.2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api group: 'mysql', name: 'mysql-connector-java', version: '5.1.44'
    implementation group: 'junit', name: 'junit', version: '4.12'
    api "br.com.simpli:simpli-model:1.1.0"
    implementation "br.com.simpli:simpli-tools:1.3.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: ossUser, password: ossPassword)
            }

            pom.project {
                name 'Simpli SQL'
                description 'Tools that makes SQL connections easier and don\'t use ORM'
                url 'https://github.com/simplitech/simpli-sql'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'gil'
                        name 'Gil Lopes Bueno'
                        email 'gil.bueno@simpli.com.br'
                    }
                    developer {
                        id 'joao.lippi'
                        name 'Joao Pedro Lippi'
                        email 'joao.lippi@simpli.com.br'
                    }
                    developer {
                        id 'ftgibran'
                        name 'Felipe Gibran Eleuterio Toledo'
                        email 'felipe.gibran@simpli.com.br'
                    }
                }
                scm {
                    connection 'scm:git:git://github.com/simplitech/simpli-sql.git'
                    developerConnection 'scm:git:ssh://github.com/simplitech/simpli-sql.git'
                    url 'https://github.com/simplitech/simpli-sql'
                }
            }
        }
    }
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}
