group 'com.agile4j'
version '1.0.5'

buildscript {
    ext.kotlin_version = '1.3.31'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'


repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.slf4j:slf4j-log4j12:1.7.2"
    compile "org.apache.commons:commons-lang3:3.5"
    compile "commons-beanutils:commons-beanutils:1.9.3"
    compile "commons-collections:commons-collections:3.2.1"
    compile "dom4j:dom4j:1.6.1"
    compile "org.yaml:snakeyaml:1.17"
    compile "org.aspectj:aspectjweaver:1.8.4"
    compile "cglib:cglib:2.2.2"
    testCompile "junit:junit:4.12"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}


publishing {
    // 定义发布什么
    publications {
        mavenJava(MavenPublication) {
            // groupId = project.group
            // artifactId = project.name
            // version = project.version
            // groupId,artifactId,version，如果不定义，则会按照以上默认值执行
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                // 构件名称
                // 区别于artifactId，可以理解为artifactName
                name = 'agile4j-utils'
                // 构件描述
                description = '工具包'
                // 构件主页
                url = 'https://github.com/agile4j/agile4j-utils'
                // 许可证名称和地址
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 开发者信息
                developers {
                    developer {
                        name = 'liurenpeng'
                        email = '13263258175@163.com'
                    }
                }
                // 版本控制仓库地址
                scm {
                    url = 'https://github.com/agile4j/agile4j-utils'
                    connection = 'scm:git:git://github.com/agile4j/agile4j-utils.git'
                    developerConnection = 'scm:git:ssh://git@github.com:agile4j/agile4j-utils.git'
                }
            }
        }
    }
    // 定义发布到哪里
    repositories {
        maven {
            //url "https://oss.sonatype.org/content/repositories/snapshots"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                // 这里就是之前在issues.sonatype.org注册的账号
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    // <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
}