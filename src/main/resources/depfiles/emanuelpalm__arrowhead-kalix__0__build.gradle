buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    group = 'se.arkalix'
    version = '0.5.2'

    tasks.withType(JavaCompile) {
        sourceCompatibility = '11'
        targetCompatibility = '11'
    }
}

// Load property from gradle.properties or return "N/A" if not defined.
def prop = { String name -> return project.hasProperty(name) ? project.property(name) : "N/A" }

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        repositories {
            maven {
                credentials {
                    // The RHS variables are collected from the user gradle.properties file.
                    username = prop("arkalixJiraUsername")
                    password = prop("arkalixJiraPassword")
                }
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
        publications {
            mavenJava(MavenPublication) {
                pom {
                    url = 'https://arkalix.se'
                    licenses {
                        license {
                            name = 'The Eclipse Public License, Version 2.0'
                            url = 'https://www.eclipse.org/legal/epl-2.0/'
                        }
                    }
                    developers {
                        developer {
                            id = 'emanuelpalm'
                            name = 'Emanuel Palm'
                            email = 'emanuel.palm@arkalix.se'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/emanuelpalm/arrowhead-kalix.git'
                        developerConnection = 'scm:git:ssh://github.com/emanuelpalm/arrowhead-kalix.git'
                        url = 'http://github.com/emanuelpalm/arrowhead-kalix/'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    afterEvaluate {
        java {
            withJavadocJar()
            withSourcesJar()
        }
        javadoc {
            exclude '**/internal/**'
        }
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                }
            }
        }
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        def console = System.console()
        console.printf("\nSignatures required. Using the following details from gradle.properties:\n")

        // The RHS variables are collected from the user gradle.properties file.
        def keyId = prop("arkalixSigningKeyId")
        def file = prop("arkalixSigningSecretKeyRingFile")
        def password = prop("arkalixSigningPassword")

        console.printf("PGP Key Id: %s\n", keyId)
        console.printf("PGP Secret Key Ring File: %s\n", file)
        console.printf("PGP Private Key Password: %s\n\n", password != null ? "***" : "-")

        allprojects {
            ext."signing.keyId" = keyId
            ext."signing.secretKeyRingFile" = file
            ext."signing.password" = password
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
