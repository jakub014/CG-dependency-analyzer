apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

group = 'uk.org.okapibarcode'
archivesBaseName = 'okapibarcode'
version = '0.2.7'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.beust:jcommander:1.78'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.zxing:javase:3.3.0'
    testImplementation 'org.reflections:reflections:0.9.10'
}

sourceCompatibility = 7
targetCompatibility = 7

// When using JDK8+ as the primary JDK, JDK7 should be installed as a secondary JDK to compile
// against (to make sure that we're not inadvertently using newer APIs), and the JDK7_HOME
// environment variable should be set to the home directory of this secondary JDK
// https://blogs.oracle.com/darcy/entry/bootclasspath_older_source
tasks.withType(JavaCompile) {
    doFirst {
        if (System.env.JDK7_HOME != null) {
            options.fork = true
            options.bootClasspath = "$System.env.JDK7_HOME/jre/lib/rt.jar"
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

jar {
    dependsOn 'check'
    manifest.attributes(
        'Main-Class': 'uk.org.okapibarcode.OkapiBarcode',
        'Implementation-Title': 'Okapi Barcode',
        'Implementation-Version': version
    )
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// Enable JaCoCo XML output for Codecov, and exclude GUI classes from the coverage reports
jacocoTestReport {
    reports {
        xml.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['uk/org/okapibarcode/*.class', 'uk/org/okapibarcode/gui/**/*.class'])
        }))
    }
}

wrapper {
    gradleVersion = '6.7'
}

/*------------------------------------------------------------------------------------------------*/
/*                         CONFIGURATION FOR UPLOAD TO MAVEN CENTRAL REPO                         */
/*                                                                                                */
/* The signing credentials and Sonatype OSSRH credentials must be in the gradle.properties file.  */
/* Upload to Maven Central with the command "gradlew uploadArchives".                             */
/* http://central.sonatype.org/pages/gradle.html                                                  */
/* http://central.sonatype.org/pages/ossrh-guide.html                                             */
/*                                                                                                */
/*------------------------------------------------------------------------------------------------*/

apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
        }
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
        pom.project {
            name 'Okapi Barcode'
            description 'Okapi Barcode is an open-source barcode generator written entirely in Java, ' +
                        'supporting over 50 encoding standards including all ISO standards.'
            packaging 'jar'
            url 'https://github.com/woo-j/OkapiBarcode'
            scm {
                connection 'scm:git:git://github.com/woo-j/OkapiBarcode.git'
                url 'https://github.com/woo-j/OkapiBarcode'
            }
            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'woo-j'
                    name 'Robin Stuart'
                }
                developer {
                    id 'gredler'
                    name 'Daniel Gredler'
                }
            }
        }
    }
}
