buildscript {
    repositories {
        google()
        mavenCentral()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.14'
        classpath 'com.github.nbaztec:coveralls-jacoco-gradle-plugin:1.2.6'
    }

    ext {
        grpc_version = '1.34.0'
        protobuf_version = '3.14.0'
        javax_annotation_version = '1.3.1'
        lombok_version = '1.18.16'
        junit_version = '5.7.0'
        jackson_version = '2.12.0'
        commons_lang_version = '3.11'
        slf4j_version = '1.7.30'
        log4j2_version = '2.14.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
}

subprojects {
    group 'games.mythical'
    version = '2.0.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencies {
        implementation group: 'org.projectlombok', name: 'lombok', version: "${lombok_version}"

        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombok_version}"

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: "${junit_version}"

        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${lombok_version}"
    }

    publishing {
        repositories {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/MythicalGames/ivi-sdk-java')
                credentials {
                    username = project.findProperty('github.user') ?: System.getenv('GITHUB_USER')
                    password = project.findProperty('github.token') ?: System.getenv('GITHUB_TOKEN')
                }
            }
            maven {
                name = 'MavenCentral'
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = project.findProperty('ossrhUsername')
                    password = project.findProperty('ossrhPassword')
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = 'IVI SDK'
                    description = 'The IVI integration SDK for Java game servers'
                    url = 'https://mythical.games'
                    from components.java

                    scm {
                        connection = 'https://github.com/MythicalGames/ivi-sdk-java.git'
                        developerConnection = 'https://github.com/MythicalGames/ivi-sdk-java.git'
                        url = 'https://github.com/MythicalGames/ivi-sdk-java.git'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            name = 'Keith Miller'
                            email = 'keith@mythical.games'
                            organization = 'Mythical Games'
                            organizationUrl = 'https://mythical.games'
                        }
                    }
                }
            }
        }
    }

    tasks.withType(Sign) {
        onlyIf { project.hasProperty('signingKey') && project.hasProperty('signingPassword') }
    }

    signing {
        // switch to this and comment out the following line to test signing locally
        // useGpgCmd()
        useInMemoryPgpKeys(project.findProperty('signingKey'), findProperty('signingPassword'))
        sign publishing.publications.mavenJava
    }
}

task printVersionToFile {
    doLast {
        file("${rootDir}/version.txt").text = "${project.version}"
    }
}