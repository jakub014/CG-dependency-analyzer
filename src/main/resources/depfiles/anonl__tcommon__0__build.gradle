buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        // Google error-prone
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.16"
    }
}

allprojects {
    group = 'nl.weeaboo.common'
    version = '3.0.3'
}

apply from: 'dependencies.gradle'

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile deps.junit
        testCompile deps.guava_testlib
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    apply from: rootProject.file('errorprone.gradle')
    apply from: rootProject.file('publish.gradle')
}

project(':tcommon-core') {
    dependencies {
        compileOnly deps.static_analysis_annotations
    }
}

project(':tcommon-apt6') {
    dependencies {
        compile project(':tcommon-core')
    }
}

project(':tcommon-entity') {
    dependencies {
        compile project(':tcommon-core')
    }
}

project(':tcommon-logging') {
    dependencies {
        compile project(':tcommon-core')
        compile deps.slf4j_api
    }
}

project(':tcommon-prefsstore') {
    dependencies {
        compile project(':tcommon-core')
    }
}

project(':tcommon-test') {
    dependencies {
        compile project(':tcommon-core')
        compile deps.junit
    }
}
