plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.kascoder'
version '0.4'
sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    compile 'org.slf4j:slf4j-api:1.7.30'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'vkclient'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Vk Client'
                description = 'Vkontakte API java client'
                url = 'https://github.com/kascoder/vkclient'
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/kascoder/vkclient/issues'
                }
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/kascoder/vkclient/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/kascoder/vkclient'
                    connection = 'scm:git:git://github.com/kascoder/vkclient.git'
                    developerConnection = 'scm:git:ssh://git@github.com:kascoder/vkclient.git'
                }
                developers {
                    developer {
                        name = 'Pavel Kasper'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
