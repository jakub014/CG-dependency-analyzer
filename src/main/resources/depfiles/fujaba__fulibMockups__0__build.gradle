plugins {
   id 'java'
   id 'maven-publish'
   // https://plugins.gradle.org/plugin/org.fulib.fulibGradle
   id 'org.fulib.fulibGradle' version '0.5.0'
   // https://plugins.gradle.org/plugin/com.bmuschko.nexus
   id 'com.bmuschko.nexus' version '2.3.1'
   // https://plugins.gradle.org/plugin/io.codearte.nexus-staging
   id 'io.codearte.nexus-staging' version '0.22.0'
}

// --------------- Info ---------------

group = 'org.fulib'
version = "git describe --tags".execute().text[1..-2] // strip v and \n
description = 'Tools for generating HTML mockups and Web Apps.'

modifyPom {
   project {
      name = project.name
      description = project.description
      url = 'https://github.com/fujaba/fulibMockups'
      inceptionYear = '2019'

      scm {
         url = 'https://github.com/fujaba/fulibMockups'
      }

      licenses {
         license {
            name = 'BSD 3-Clause'
            url = 'https://opensource.org/licenses/BSD-3-Clause'
         }
      }

      developers {
         developer {
            id = 'Clashsoft'
            name = 'Adrian Kunz'
            email = 'clashsoft@hotmail.com'
         }

         developer {
            id = 'azuendorf'
            name = 'Albert ZÃ¼ndorf'
            email = 'zuendorf@uni-kassel.de'
         }
      }
   }
}

sourceCompatibility = 1.8

// --------------- Dependencies ---------------

repositories {
   mavenLocal()
   mavenCentral()
}

dependencies {
   // https://mvnrepository.com/artifact/org.fulib/fulibScenarios
   fulibScenarios group: 'org.fulib', name: 'fulibScenarios', version: '1.7.0'

   // https://mvnrepository.com/artifact/org.fulib/fulibYaml
   implementation group: 'org.fulib', name: 'fulibYaml', version: '1.5.0'

   // https://mvnrepository.com/artifact/org.antlr/ST4
   implementation group: 'org.antlr', name: 'ST4', version: '4.3.1'

   // https://mvnrepository.com/artifact/com.codeborne/selenide
   implementation group: 'com.codeborne', name: 'selenide', version: '5.19.0'

   // https://mvnrepository.com/artifact/com.sparkjava/spark-core
   implementation group: 'com.sparkjava', name: 'spark-core', version: '2.9.3'

   // https://mvnrepository.com/artifact/org.fulib/fulibTools
   testImplementation group: 'org.fulib', name: 'fulibTools', version: '1.5.1'

   // https://mvnrepository.com/artifact/junit/junit
   testImplementation group: 'junit', name: 'junit', version: '4.13'
}

// --------------- FulibGradle Overrides ---------------

final File testDir = file "$project.buildDir/generated/sources/scenarios/test/"

sourceSets.test.java.srcDir(testDir)

generateScenarioSource {
   testDirectory = testDir
   classDiagramSVG = true
}

generateTestScenarioSource {
   dependsOn 'classes'
   classpath = sourceSets.main.output
   modelDirectory = testDir
   testDirectory = testDir
}
