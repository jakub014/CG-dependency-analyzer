plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow'
    id 'maven-publish'
    id 'signing'
}

group = 'eu.cyber-geiger'
description = 'localStorage'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    checkstyleConfig
}

dependencies {
    // H2SQL
    implementation 'com.h2database:h2:1.4.200'

    compile group: 'com.puppycrawl.tools', name: 'checkstyle', version: '8.38'
    checkstyleConfig("com.puppycrawl.tools:checkstyle:8.38") { transitive = false }

    // junit
    testImplementation("junit:junit:4.13")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.7.0")
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.12.1'

    // Picocli
    implementation 'info.picocli:picocli:4.5.2'
    annotationProcessor 'info.picocli:picocli-codegen:4.5.2'
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.0.b2'
    }
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'ch.fhnw.geiger.localstorage.CommandLine'
        )
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}


jar.dependsOn shadowJar
jar.dependsOn test

check.dependsOn checkstyleMain

/* integrate junit */
test {
    systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
    useJUnitPlatform {
        // includeTestsMatching "ch.fhnw.geiger.localstorage.Test*"
    }
    maxHeapSize = '1G'
}

checkstyle {
    toolVersion '8.38'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
    ignoreFailures = false
    maxWarnings = 0
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}


java {
    withSourcesJar()
    withJavadocJar()
}

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        // The repository to publish to, Sonatype/MavenCentral
        maven {
            // This is an arbitrary name, you may also use "mavencentral" or
            // any other name that's descriptive for you
            name = "sonatype"

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = gitBranch() == "main" ? releasesRepoUrl : snapshotsRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'toolbox-storage'
            from components.java

            version = gitBranch() == "main" ? version : version + '-SNAPSHOT'

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'toolbox-storage'
                description = 'The storage module of the GEIGER toolbox'
                url = 'https://github.com/cyber-geiger/toolbox-storage'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mgwerder'
                        name = 'Martin Gwerder'
                        email = 'martin.gwerder@fhnw.ch'
                    }
                    developer {
                        id = 'sleemann'
                        name = 'Sacha Leemann'
                        email = 'sacha.leemann@fhnw.ch'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cyber-geiger/toolbox-storage.git'
                    developerConnection = 'scm:git:ssh://github.com/cyber-geiger/toolbox-storage.git'
                    url = 'http://github.com/cyber-geiger/toolbox-storage/'
                }
            }

        }

    }

}

signing {
    sign publishing.publications.mavenJava
}


