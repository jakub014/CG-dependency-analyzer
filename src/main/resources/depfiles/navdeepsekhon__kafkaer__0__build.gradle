plugins {
    id 'java'
}

apply plugin: 'maven'
apply plugin: 'signing'

group 'co.navdeep'
version '1.4.2'
archivesBaseName = "kafkaer"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven{ url "http://packages.confluent.io/maven/"}
}

dependencies {
    compile "org.apache.kafka:kafka-clients:2.1.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.8"
    compile "org.apache.commons:commons-text:1.6"
    compile "commons-io:commons-io:2.6"
    compile "org.apache.commons:commons-configuration2:2.4"
    compile "commons-beanutils:commons-beanutils:1.9.3"
    compile "args4j:args4j:2.33"
    compile "org.slf4j:slf4j-simple:1.7.30"
    compile "io.confluent:kafka-schema-registry-client:5.5.1"

    compileOnly "org.projectlombok:lombok:1.18.6"
    annotationProcessor "org.projectlombok:lombok:1.18.6"
    
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:3.4.0"
}

task execJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'kafkaer',
                'Implementation-Version': version,
                'Main-Class': 'co.navdeep.kafkaer.App'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

jar {
    manifest {
        attributes 'Implementation-Title': 'kafkaer',
                'Implementation-Version': version,
                'Main-Class': 'co.navdeep.kafkaer.App'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

signing {
    sign configurations.archives
}

def usr = ''
def pwd = ''
if(project.hasProperty("nexusUsername")){
    usr = nexusUsername
    pwd = nexusPassword
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: usr, password: pwd)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: usr, password: pwd)
            }

            pom.project {
                name 'kafkaer'
                packaging 'jar'
                description 'Deployment automation utility for apache kafka. Automate kafka cluster configurations for topics, brokers, ACLs. This jar can be used as an executable as well as a maven dependecy'
                url 'https://github.com/navdeepsekhon/kafkaer'

                scm {
                    connection 'git@github.com:navdeepsekhon/kafkaer.git'
                    developerConnection 'git@github.com:navdeepsekhon/kafkaer.git'
                    url 'https://github.com/navdeepsekhon/kafkaer'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'navdeep'
                        name 'Navdeep Sekhon'
                        email 'hi@navdeep.co'
                    }
                }
            }
        }
    }
}