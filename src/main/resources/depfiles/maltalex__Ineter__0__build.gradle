plugins{
	id 'com.github.ben-manes.versions' version '0.36.0'
	id "me.champeau.gradle.jmh" version "0.5.2"
	id "com.diffplug.spotless" version "5.8.2"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

apply from: 'jacoco.gradle'
apply from: 'publish.gradle'

sourceCompatibility = 8
if(JavaVersion.current() != JavaVersion.VERSION_1_8){
	compileJava {
		options.compilerArgs.addAll(['--release', '8'])
	}
}

repositories {
	mavenCentral()
}

def guavaDep = 'com.google.guava:guava:30.1-jre'
dependencies {
	jmh guavaDep
	testImplementation guavaDep
	testImplementation 'org.junit.platform:junit-platform-runner:1.7.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
}

spotless {
	java {
		/*
		 The ineter format is the default Ecplise 4.15.0 (2020-03) format with on/off tags (@formatter:on/off) enabled
		 To use the formatter in intellij, import the xml into https://plugins.jetbrains.com/plugin/6546-eclipse-code-formatter
		 */
		eclipse().configFile 'spotless/ineter-java-format.xml'
		licenseHeaderFile 'spotless/license-header'
		importOrder 'java', 'javax', 'org', 'com'
		removeUnusedImports()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
}

test {
	useJUnitPlatform()
}

wrapper {
	gradleVersion = '6.7.1'
}