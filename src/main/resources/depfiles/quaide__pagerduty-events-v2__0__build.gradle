plugins {
    id 'java'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.5.1'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'maven-publish'
    id 'signing'
}

version '1.0-SNAPSHOT'

sourceCompatibility = 1.14

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.2'
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.3.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.10.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.2'
    implementation 'org.pitest:pitest-junit5-plugin:0.12'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.12.0'

}

check.dependsOn verifyGoogleJavaFormat

test {
    useJUnitPlatform()
    dependsOn 'cleanTest'
    testLogging {
        events "passed", "skipped", "failed"
    }
}

pitest {
    threads = 4
    outputFormats = ['HTML']
    mutationThreshold = 100
    targetClasses = ['pagerdutyevents.*']
    testPlugin = "junit5"
}

check.dependsOn 'pitest'

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}
test.finalizedBy jacocoTestReport

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.quaide'
            artifactId = 'pagerduty-events-v2-client'
            version = '1.0.2'

            from components.java

            pom {
                name = 'PagerDuty Events v2 Client'
                description = 'The public API for PagerDuty Events v2 API for incident event monitoring system.'
                url = 'https://github.com/quaide/pagerduty-events-v2'
                licenses {
                    license {
                        name = 'Mit License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        name = 'Quaide Tranter'
                        organizationUrl = 'http://www.quaidetranter.com'
                    }
                    developer {
                        name = 'Mitchell Irvin'
                        organizationUrl = 'http://mitchell-irvin.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/quaide/pagerduty-events-v2.git'
                    developerConnection = 'scm:git:ssh://github.com/quaide/pagerduty-events-v2'
                    url = 'https://github.com/quaide/pagerduty-events-v2/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = project.properties.ossrhUsername
                password = project.properties.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

