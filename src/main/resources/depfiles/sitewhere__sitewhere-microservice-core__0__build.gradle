plugins {
	id 'java'
	id 'distribution'
	id 'com.github.hierynomus.license' version '0.15.0' 
	id 'org.ajoberstar.grgit' version '2.2.1' 
}

ext {
	git = org.ajoberstar.grgit.Grgit.open(currentDir: file('.'))
}

ext['git.revision'] = ext.git.head().id
ext['git.revision.abbrev'] = ext.git.head().abbreviatedId

ext['sitewhere.k8s.api.version'] = '3.0.1'
ext['sitewhere.grpc.api.version'] = '3.0.0'
ext['jackson.version'] = '2.9.10'

apply plugin: 'distribution'
	
allprojects {
	group = 'com.sitewhere'
	version = '3.0.11'
	
	repositories {
		maven { url "https://repo.maven.apache.org/maven2" }
		mavenLocal()
	}
}

subprojects {
	apply plugin: "com.github.hierynomus.license"
	license {
		header rootProject.file('HEADER')
		include "**/*.java"
	}

	// Choose Java settings.
	apply plugin: 'java'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	
	// Common dependencies used for all projects.
	dependencies {
		compile platform('io.quarkus:quarkus-bom:1.7.2.Final')
		
		compile group: 'org.graalvm.nativeimage', name: 'svm', version: '20.2.0'
		
		testCompile group: 'junit', name: 'junit', version: '4.12'
	}
}
