plugins {
    id 'java-library'
    id 'biz.aQute.bnd.builder'
}

description = 'Trellis cloud extension'

ext {
    moduleName = 'org.trellisldp.ext.aws'
}

dependencies {
    implementation platform("org.trellisldp:trellis-bom:${trellisVersion}")

    api "jakarta.enterprise:jakarta.enterprise.cdi-api:$cdiApiVersion"
    api "jakarta.inject:jakarta.inject-api:$injectApiVersion"
    api "org.apache.commons:commons-rdf-api:$commonsRdfVersion"
    api "com.amazonaws:aws-java-sdk-s3:$awsVersion"
    api "com.amazonaws:aws-java-sdk-sns:$awsVersion"
    api "org.trellisldp:trellis-api"
    api "org.trellisldp:trellis-http"

    implementation "commons-io:commons-io:$commonsIoVersion"
    implementation "org.apache.commons:commons-compress:$commonsCompressVersion"
    implementation "org.apache.jena:jena-arq:$jenaVersion"
    implementation "org.apache.jena:jena-commonsrdf:$jenaVersion"
    implementation "org.eclipse.microprofile.config:microprofile-config-api:$microprofileConfigVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "org.trellisldp:trellis-vocabulary"

    testImplementation "org.apache.commons:commons-text:$commonsTextVersion"
    testImplementation "io.smallrye.config:smallrye-config:$smallryeConfigVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

    testImplementation "org.trellisldp:trellis-notification-jackson"
    testImplementation "org.trellisldp:trellis-constraint"

    testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
}

test {
    systemProperty "trellis.test.aws", project.findProperty("trellis.test.aws") ?: System.getenv("TEST_AWS")
    systemProperty "trellis.aws.memento-bucket", System.getenv("AWS_TEST_BUCKET") ?: "test.trellisldp.org"
    systemProperty "trellis.aws.binary-bucket", System.getenv("AWS_TEST_BUCKET") ?: "test.trellisldp.org"
    systemProperty "trellis.aws.topic", System.getenv("AWS_TEST_TOPIC") ?: "test-topic"
}

