buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    }
}

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm"
    id 'maven'
}

description = 'RawHTTP core module'
version rawHttpCoreVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "org.jetbrains.kotlin:kotlin-reflect"
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
    testCompile "io.kotlintest:kotlintest:2.0.7"
    testCompile 'com.sparkjava:spark-core:2.9.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

jar {
    manifest {
        attributes 'Implementation-Version': version,
                'Implementation-Name': 'RawHTTP Core',
                'Implementation-Vendor': 'Renato Athaydes',
                'Automatic-Module-Name': 'rawhttp.core'
    }
}

/* Publishing config */

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure(install.repositories.mavenInstaller) {
    pom.project {
        inceptionYear '2017'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://renatoathaydes.github.io/rawhttp'

        scm {
            connection 'git@github.com:renatoathaydes/rawhttp.git'
            developerConnection 'git@github.com:renatoathaydes/rawhttp.git'
            url 'https://github.com/renatoathaydes/rawhttp'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

final rawHttpGradle = new RawHttpGradle(project)

bintray {
    user = rawHttpGradle.getGradleProperty("bintrayUserName")
    key = rawHttpGradle.getGradleProperty("bintrayApiKey")
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'rawhttp-core'
        licenses = ['Apache-2.0']
        desc = project.description
        vcsUrl = 'https://github.com/renatoathaydes/rawhttp.git'
        websiteUrl = 'https://renatoathaydes.github.io/rawhttp'
        issueTrackerUrl = 'https://github.com/renatoathaydes/rawhttp/issues'
        labels = ['http', 'java', 'http-parser']
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = rawHttpGradle.getGradleProperty("ossrhUsername")
                password = rawHttpGradle.getGradleProperty("ossrhPassword")
                close = '1' // '0' to NOT close
            }
        }
    }

}

bintrayUpload.dependsOn build, sourcesJar
