group = rootProject.group
version = rootProject.version

apply plugin: 'scala'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.github.johnrengelman.shadow"

ext {
    artefact = 'avro-formatter-kafka_' + project(':kafka').baseKafkaVersion + '-scala_' + rootProject.baseScalaVersion
    serdeArtefact = 'avro-serde-kafka_' + project(':kafka').baseKafkaVersion + '-scala_' + rootProject.baseScalaVersion
}

artifacts {
    archives shadowJar
}

if (rootProject.hasProperty('maven-central')) {
    uploadArchives.repositories.mavenDeployer.pom.artifactId = artefact
}
publishing {
    publications {
        maven(MavenPublication) {
            artifactId artefact
            from components.java
        }
    }
}

install.repositories.mavenInstaller.pom.artifactId = artefact
def installer = install.repositories.mavenInstaller
[installer]*.pom*.whenConfigured { pom ->
    pom.dependencies.find {dep -> dep.groupId == 'io.amient.affinity' && dep.artifactId == 'avro-serde-kafka' }.artifactId = serdeArtefact
}

assemble.dependsOn shadowJar
uploadArchives.dependsOn assemble

dependencies {
    compile project(':kafka:avro-serde-kafka')
    provided group: 'org.apache.kafka', name: 'kafka_' + rootProject.baseScalaVersion, version: project(':kafka').kafkaVersion
}



