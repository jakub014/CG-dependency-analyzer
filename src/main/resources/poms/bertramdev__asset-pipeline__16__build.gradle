buildscript {
    repositories {
        // mavenLocal()
        jcenter()
        maven {
            url "http://xbib.org/repository"
        }
    }
    dependencies {
        classpath 'org.xbib.gradle.plugin:gradle-plugin-jflex:1.1.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'idea'
apply plugin: 'org.xbib.gradle.plugin.jflex'

group = 'com.bertramlabs.plugins'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()

}

configurations {
    provided
}
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    api project(':asset-pipeline-core')
    implementation 'org.codehaus.groovy:groovy:2.4.19'
    api 'org.mozilla:rhino:1.7R4'
    // api 'log4j:log4j:1.2.17'

    testImplementation "org.spockframework:spock-core:1.3-groovy-2.4"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'jsx-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'jsx-asset-pipeline'
                    description 'JSX Transpiler Extension for the JVM Based Asset-Pipeline.'
                    url 'https://github.com/bertramdev/asset-pipeline'
                    scm {
                        url 'https://github.com/bertramdev/asset-pipeline'
                        connection 'scm:https://bertramdev@github.com/bertramdev/asset-pipeline.git'
                        developerConnection 'scm:git://github.com/bertramdev/asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
            afterEvaluate {
                artifact sourcesJar
                artifact javadocJar    
            }
        }
    }
}

bintray {
     if(project.hasProperty('bintrayUser')) {
        user = bintrayUser
        key = bintrayKey
    }
    publish=true
    publications = ['maven']
    pkg {
        repo = 'asset-pipeline'
        userOrg = 'bertramlabs'
        name = 'jsx-asset-pipeline'
        vcsUrl = 'https://github.com/bertramdev/asset-pipeline.git'
        licenses = ['Apache-2.0']
        version {
            if (project.hasProperty('mavenUser')) {
                mavenCentralSync {
                    sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
                    user = mavenUser //OSS user token
                    password = mavenPassword //OSS user password
                    close = '1'
                    //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
                }
            }
        }
    }
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
