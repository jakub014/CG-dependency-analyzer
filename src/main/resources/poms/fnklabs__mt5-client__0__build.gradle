buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    group = 'com.fnklabs.mt5-client'
    version = "${version}"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    ext {
        vers = [
                commonscodec: '1.11',
                commonsio   : '1.3.2',
                httpclient  : '5.0.3',
                guava       : '20.0',
                jackson     : '2.8.10',
                jodatime    : '2.9.1',
                junit       : '5.7.0',
                intellij    : '20.1.0',
                lang3       : '3.5',
                logback     : '1.1.11',
                metrics     : '0.1.14',
                mockito     : '3.6.0',
                pool2       : '2.8.1',
                slf4j       : '1.1.11',
        ]
    }

    configurations {
        all*.exclude module: 'slf4j-log4j12'
        all*.exclude module: 'slf4j-simple'
        all*.exclude group: 'com.codahale.metrics'
    }

    dependencies {
        testCompile "org.junit.jupiter:junit-jupiter-api:$vers.junit"
        testCompile "org.junit.jupiter:junit-jupiter-params:$vers.junit"
        testCompile "org.junit.jupiter:junit-jupiter-engine:$vers.junit"
        testCompile "org.junit.vintage:junit-vintage-engine:$vers.junit"

        testCompile "org.mockito:mockito-core:$vers.mockito"
        testCompile "org.mockito:mockito-junit-jupiter:$vers.mockito"
    }


    test {
        useJUnit()
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'MT5 Client'
                    description = 'MT5 API client library'
                    url = 'https://github.com/fnklabs/mt5-client'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'a.alimov'
                            name = 'Alisher Alimov'
                            email = 'alisher.alimov@fnklabs.com'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:fnklabs/mt5-client.git'
                        developerConnection = 'scm:git:ssh:git@github.com:fnklabs/mt5-client.git'
                        url = 'https://github.com/fnklabs/mt5-client'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = "$sonatypeUsername"
                    password = "$sonatypePassword"
                }
            }
        }
    }

    signing {
        required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.mavenJava
    }


    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}
