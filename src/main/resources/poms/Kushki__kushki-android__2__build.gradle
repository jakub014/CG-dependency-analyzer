import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    ext.kotlin_version = '1.0.5-2'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.1'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'
group = 'com.kushkipagos'
version '1.4.0'

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

repositories {
    jcenter()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android:android:4.1.1.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile 'org.slf4j:slf4j-nop:1.7.21'
    testCompile 'org.mockito:mockito-core:1.10.19'
    // Source: https://git.io/vPL8S
    testCompile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    testCompile('com.github.tomakehurst:wiremock:2.1.12') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

task unitTest(type: Test) {
    include 'com/kushkipagos/android/**'
    exclude 'com/kushkipagos/android/integration/**'
}

task integrationTest(type: Test) {
    include 'com/kushkipagos/android/integration/**'
}

ext {
    license = 'MIT'
    licenseUrl = 'https://github.com/Kushki/kushki-android/blob/master/LICENSE.txt'
    gitUrl = 'https://github.com/Kushki/kushki-android.git'
    scmUrl = 'scm:git:' + gitUrl
    packageName = 'kushki-android'
    packageDescription = 'Librer√≠a para integrarse con Kushki desde Android'
    versionTag = version
}

install {
    repositories.mavenInstaller {
        pom.artifactId = packageName
        pom.version = versionTag
        pom.project {
            name packageName
            description packageDescription
            url gitUrl
            organization {
                name 'Kushki S.A.'
                url 'https://www.kushkipagos.com'
            }
            developers {
                developer {
                    id 'kushki'
                    name 'kushki'
                    email 'dev@kushkipagos.com'
                }
            }
            licenses {
                license {
                    name license
                    url licenseUrl
                    distribution 'repo'
                }
            }
            scm {
                url gitUrl
                connection scmUrl
                developerConnection scmUrl
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = packageName
        desc = packageDescription
        userOrg = user
        licenses = [license]
        vcsUrl = gitUrl
        websiteUrl = gitUrl
        publicDownloadNumbers = false
        version {
            name = versionTag
            desc = packageDescription
            vcsTag = 'v' + versionTag
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = System.getenv('MAVEN_CENTRAL_USER')
                password = System.getenv('MAVEN_CENTRAL_TOKEN')
                close = '1'
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task printVersion << {
    println project.version
}

tasks.withType(Test) {
    outputs.upToDateWhen { false }
    testLogging {
        events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR, TestLogEvent.STANDARD_OUT
    }
}