plugins {
    id 'com.google.osdetector' version '1.4.0'
    id 'com.github.sherter.google-java-format' version '0.9' apply false
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
    id 'com.jfrog.artifactory' version '4.18.3' apply false
    id 'com.jfrog.bintray' version '1.8.5' apply false
    id 'me.champeau.gradle.jmh' version '0.5.0' apply false
    id 'io.morethan.jmhreport' version '0.9.0' apply false
    id 'com.google.protobuf' version '0.8.14' apply false
    id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
}

apply from: 'artifactory.gradle'
apply from: 'bintray.gradle'
apply from: 'publication.gradle'

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.github.sherter.google-java-format'

    apply plugin: 'maven-publish'

    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'com.jfrog.bintray'

    apply from: "${project.rootDir}/dependencies-managment.gradle"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        if (project.hasProperty('versionSuffix')) {
            project.version += project.getProperty('versionSuffix')
        }
    }

    repositories {
        mavenLocal()
        maven { url 'https://oss.jfrog.org/oss-snapshot-local' }
        mavenCentral()
        if (version.endsWith('SNAPSHOT') || project.hasProperty('platformVersion')) {
            maven { url 'http://repo.spring.io/libs-snapshot' }
            maven { url 'https://oss.jfrog.org/oss-snapshot-local' }
        }
    }

    googleJavaFormat {
        toolVersion = '1.6'
        exclude '**/generated'
    }
}
