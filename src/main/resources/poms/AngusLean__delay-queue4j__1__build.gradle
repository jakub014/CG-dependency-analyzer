apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply from: '../publish.gradle'

version '1.0.0'

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.25'

    implementation 'org.projectlombok:lombok:1.18.10'
    implementation 'org.redisson:redisson:3.11.5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.0.1'

    testImplementation 'org.redisson:redisson:3.11.5'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.slf4j:slf4j-simple:1.7.25'

    annotationProcessor 'org.projectlombok:lombok:1.18.10'

}


publishing {
    // 定义发布什么
    publications {
        mavenJavaCenter(MavenPublication) {
            groupId = 'cn.anguslean'
            artifactId = 'delay-quene4j'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'delay-queue4j'
                description = 'Simple Distributed Delay Queue For Redis In Java'
                url = 'https://github.com/AngusLean/delay-queue4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'cupoghost'
                        email = 'cupoghost@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/AngusLean/delay-queue4j'
                    connection = 'scm:git:git@github.com:AngusLean/delay-queue4j.git'
                    developerConnection = 'scm:git:ssh://git@github.com:AngusLean/delay-queue4j.git'
                }
            }
        }
    }
    // 定义发布到哪里
    repositories {
        maven {
            url version.endsWith("SNAPSHOT") ? "https://oss.sonatype.org/content/repositories/snapshots":
                    "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}
signing {
    sign publishing.publications.mavenJavaCenter
}


