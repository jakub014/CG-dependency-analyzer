plugins {
    id "java-library"
    id "org.openjfx.javafxplugin" version "0.0.9"
    id "maven-publish"
    id "signing"
}

group "bayern.steinbrecher"
version "1.61-rc.11-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

sourceCompatibility = 15
targetCompatibility = 15

dependencies {
    compileOnly "org.jetbrains:annotations:19.0.0"
    implementation "bayern.steinbrecher:JavaUtility:0.18-rc.8-SNAPSHOT"
    implementation "com.google.guava:guava:30.1-jre"
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
        withSourcesJar()
        withJavadocJar()
    }
}

javafx {
    version = "$sourceCompatibility"
    modules = [
            "javafx.controls",
            "javafx.fxml",
            "javafx.graphics"
    ]
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${ossrhUsername}"
                password = "${ossrhPassword}"
            }
        }
    }
    publications {
        library(MavenPublication) {
            pom {
                name = 'GenericWizard'
                description = 'A generalized, flexible, reusable and dynamic wizard implementation for JavaFX.'
                url = 'https://www.steinbrecher.bayern/projects/mavenPackages.html#genericWizard'
                licenses {
                    license {
                        name = 'GPL-3.0'
                    }
                }
                developers {
                    developer {
                        id = 'stefanHuber'
                        name = 'Stefan Huber'
                        email = 'stefan.huber.niedling@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TrackerSB/GenericWizard.git'
                    developerConnection = 'scm:git:ssh://github.com/TrackerSB/GenericWizard.git'
                    url = 'https://steinbrecher.bayern'
                }
            }
            from components.java
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.library
}
