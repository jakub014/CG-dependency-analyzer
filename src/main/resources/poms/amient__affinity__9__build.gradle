group = rootProject.group
version = rootProject.version

apply plugin: 'scala'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.github.johnrengelman.shadow"

ext {
    artefact = 'storage-kafka_' + project(':kafka').baseKafkaVersion + '-scala_' + baseScalaVersion
    serdeArtefact = 'avro-serde-kafka_' + project(':kafka').baseKafkaVersion + '-scala_' + rootProject.baseScalaVersion
    coreArtefact = 'core-scala_'+ rootProject.baseScalaVersion
}
if (rootProject.hasProperty('maven-central')) {
    uploadArchives.repositories.mavenDeployer.pom.artifactId = artefact
}
publishing {
    publications {
        maven(MavenPublication) {
            artifactId artefact
            from components.java
        }
    }
}

install.repositories.mavenInstaller.pom.artifactId = artefact
def installer = install.repositories.mavenInstaller
[installer]*.pom*.whenConfigured { pom ->
    pom.dependencies.find {dep -> dep.groupId == 'io.amient.affinity' && dep.artifactId == 'core' }.artifactId = coreArtefact
    pom.dependencies.find {dep -> dep.groupId == 'io.amient.affinity' && dep.artifactId == 'avro-serde-kafka' }.artifactId = serdeArtefact
}

assemble.dependsOn shadowJar

dependencies {
    compile project(':core')
    compile project(':kafka:avro-serde-kafka')
    compile group: 'org.apache.kafka', name: 'kafka-clients', version: project(':kafka').kafkaVersion
    testCompile project(':kafka:test-util-kafka')
    testCompile group: 'org.apache.kafka', name: 'kafka-streams', version: project(':kafka').kafkaVersion
}



