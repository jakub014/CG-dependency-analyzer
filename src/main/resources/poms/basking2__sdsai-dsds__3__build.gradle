plugins {
    id 'java-library'
    // Sonatype's Nexus doensn't support the stronger cryptographic hashes.
    // id 'maven-publish'
    id 'maven'
    id 'signing'
    id 'eclipse'
}

dependencies {
    implementation project(':sdsai-dsds-core')
    implementation platform('software.amazon.awssdk:bom:2.16.1')
    implementation 'software.amazon.awssdk:s3:2.16.1'
    testImplementation 'junit:junit:4.13.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: releaseRepository) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotRepository) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'SDSAI DSDS S3'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Simple datastructures and algorithms data store data structures.'
                    url 'https://github.com/basking2/sdsai-dsds'

                    scm {
                        connection 'scm:git:git@github.com:basking2/sdsai-dsds.git'
                        developerConnection 'git@github.com:basking2/sdsai-dsds.git'
                        url 'https://github.com/basking2/sdsai-dsds.git'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'basking2'
                            name 'Sam'
                            email 'basking2@yahoo.com'
                        }
                    }
                }
            }
        }
    }
}

