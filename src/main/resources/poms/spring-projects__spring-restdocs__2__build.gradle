description = 'Spring REST Docs Asciidoctor Extension'

configurations {
	merge
	testRuntime { extendsFrom merge }
}

dependencies {
	merge project(':spring-restdocs-asciidoctor-1.5')
	merge project(':spring-restdocs-asciidoctor-1.6')
	merge project(':spring-restdocs-asciidoctor-2.x')
	testCompile 'org.apache.pdfbox:pdfbox'
	testCompile 'org.asciidoctor:asciidoctorj:1.5.8.1'
	testCompile 'org.assertj:assertj-core'
	testCompile 'junit:junit'
	testCompile 'org.springframework:spring-core'
	testRuntime 'org.asciidoctor:asciidoctorj-pdf'
}

jar {
	dependsOn ':spring-restdocs-asciidoctor-1.5:jar'
	dependsOn ':spring-restdocs-asciidoctor-1.6:jar'
	dependsOn ':spring-restdocs-asciidoctor-2.x:jar'
	from configurations.merge.collect { file -> zipTree(file) }
}

matrixTest {
	asciidoctorj {
		group = 'org.asciidoctor'
		artifact = 'asciidoctorj'
		versions = [ asciidoctorj16Version,
			asciidoctorj20Version,
			asciidoctorj21Version,
			asciidoctorj22Version,
			asciidoctorj23Version,
			asciidoctorj24Version
		]
	}
}