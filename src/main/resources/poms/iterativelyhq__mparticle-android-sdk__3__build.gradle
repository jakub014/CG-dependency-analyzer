ext {
    kitDescription = 'mParticle Kit library for extending the Core mParticle SDK.'
}

apply plugin: 'com.android.library'
apply from: '../scripts/maven.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    testBuildType 'debug'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 28

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {

        debug {
            minifyEnabled false
            //multidex is required for instrumented test builds
            multiDexEnabled true
            buildConfigField 'Boolean', 'MP_DEBUG', 'true'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task kitSdkJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    source += 'build/generated/source/buildConfig/release/'
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    title = 'mParticle Android Kit API Reference'
    failOnError false
    include {
        String filePath = it.toString()
        filePath.contains('KitIntegration.java') ||
                filePath.contains('KitUtils')
    }
    getOptions().setNoTimestamp(true)
}

dependencies {
    implementation project(':android-core')

    testImplementation 'junit:junit:4.12'
    testImplementation  files('libs/java-json.jar')
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'org.powermock:powermock-core:2.0.2'
    testImplementation 'com.android.support:support-v4:27.1.1'
    testImplementation project(':testutils')

    androidTestImplementation project(':testutils')
    implementation 'com.android.support:support-annotations:28.0.0'
}
