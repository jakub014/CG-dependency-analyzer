/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.3/userguide/tutorial_java_projects.html
 */

buildscript {
    repositories {
        mavenCentral()
    }

}
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.7"
    }
}


// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "net.ltgt.apt"
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    apt 'org.immutables:value:2.5.6'
    compileOnly 'org.immutables:value:2.5.6:annotations'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.6'
    compile 'org.asynchttpclient:async-http-client:2.6.0'
    compile 'commons-io:commons-io:2.6'
    testCompile('org.assertj:assertj-core:3.6.2')
    testCompile('org.mockito:mockito-core:2.23.0')
    testCompile('junit:junit:4.4')
}



publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.bandwidth.sdk'
            artifactId = 'messaging'
            version = '0.10.0-SNAPSHOT'
            from components.java
        }
    }
}

//Taken and changed from http://weibeld.net/java/publish-to-maven-central.html#deploy-to-ossrh

//To deploy, you need to define the following in your local ~/.gradle/gradle.properties file
//signing.keyId
//signing.password
//signing.secretKeyRingFile
//ossrhUsername
//ossrhPassword

//To deploy, run
//gradle uploadArchives

//If you don't remove the -SNAPSHOT, the deploy will only go to the staging repo

if (project.hasProperty("signing.keyId")) {
    // Signing
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }


    // Deploying
    apply plugin: 'maven'

    // Add Javadoc JAR and sources JAR to artifact
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    artifacts {
        archives javadocJar, sourcesJar
    }

    // Configure group ID, artifact ID, and version
    group = "com.bandwidth.sdk"
    archivesBaseName = "messaging"
    version = "0.10.0-SNAPSHOT"

    // Build, sign, and upload
    uploadArchives {
        repositories {
            mavenDeployer {

                // Sign POM
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                // Destination
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    name 'Bandwidth Java Messaging'
                    packaging 'jar'
                    description 'Java SDK for Bandwidth Messaging'
                    url 'https://github.com/Bandwidth/messaging-java-sdk'

                    scm {
                        connection 'scm:git:git@github.com:Bandwidth/messaging-java-sdk.git'
                        developerConnection 'scm:git:git@github.com:Bandwidth/messaging-java-sdk.git'
                        url 'git@github.com:Bandwidth/messaging-java-sdk.git' 
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'dx-bandwidth'
                            name 'DX-Bandwidth'
                            email 'dx@bandwidth.com'
                            organization 'bandwidth'
                            organizationUrl 'http://bandwidth.com'
                        }
                    }
                }
            }
        }
    }
}
