/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020 Vladimir Orany.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.java-project'
    id 'org.kordamp.gradle.bintray'
    id 'org.kordamp.gradle.checkstyle'
    id 'org.kordamp.gradle.codenarc'
    id 'org.kordamp.gradle.coveralls'
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = '**UNDEFINED**'
if (!project.hasProperty('bintrayApiKey'))   ext.bintrayApiKey   = '**UNDEFINED**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = 'Spreadsheet Builder'
        vendor      = 'Vladimir Orany'
        description = 'DSL for Writing Spreadsheets on the JVM'

        links {
            website      = "https://github.com/${slug}"
            issueTracker = "https://github.com/${slug}/issues"
            scm          = "https://github.com/${slug}.git"
        }

        people {
            person {
                id    = 'musketyr'
                name  = 'Vladimir Orany'
                roles = ['developer']
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${project.rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${project.rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    publishing {
        signing             = false
        releasesRepository  = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }

    bintray {
        credentials {
            username = bintrayUsername
            password = bintrayApiKey
        }
        userOrg    = 'dsl-builders'
        name       = rootProject.name
        publish    = true
    }

    quality {
        checkstyle {
            toolVersion = '8.27'
        }

        codenarc {
            toolVersion = '1.5'
        }
    }

    docs {
        groovydoc {
            enabled = false
            aggregate {
                enabled = false
            }
        }
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }

    license {
        exclude '**/*.json'
        exclude '***.yml'
    }
}

subprojects { subproject ->
    if (subproject.name == 'guide') return

    apply plugin: 'groovy'

    config {
        bintray {
            name = subproject.name
            githubRepo = slug
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    dependencies {
        testCompile "org.spockframework:spock-core:$spockVersion"
    }

    jar {
        manifest.attributes provider: 'gradle'
    }
}
