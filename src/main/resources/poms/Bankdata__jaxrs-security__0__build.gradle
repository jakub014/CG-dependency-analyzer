
group 'dk.bankdata.jaxrs'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle {
    toolVersion = '8.12'
}

test {
    environment "CORR_ID_HEADER_NAME", "x-correlation-id"
    environment "CLIENT_CORR_ID_HEADER_NAME", "x-client-correlation-id"
    environment "HEADER_FORWARDING", "some-header-1,some-header-2"
    jacoco {
        includes = ['*.*']
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly(group: 'javax', name: 'javaee-api', version: project['javax.version'])
    compile(group: 'org.slf4j', name: 'slf4j-api', version: project['org.slf4j.version'])
    compileOnly(group: 'org.slf4j', name: 'slf4j-log4j12', version: project['org.slf4j.version'])
    compile(group: 'org.bitbucket.b_c', name: 'jose4j', version: project['org.bitbucket.b_c.version'])
    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project['com.fasterxml.jackson.version'])
    compile(group: 'dk.bankdata.api', name:'types', version: project['dk.bankdata.api.version'])
    compile(group: 'io.opentracing', name:'opentracing-api', version: project['io.opentracing.version'])
    compile(group: 'io.opentracing', name:'opentracing-util', version: project['io.opentracing.version'])
    
    testCompile(group: 'javax', name: 'javaee-api', version: project['javax.version'])
    testCompile(group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project['org.junit.jupiter.version'])
    testCompile(group: 'org.mockito', name: 'mockito-core', version: project['org.mockito.version'])
    testCompile(group: 'com.github.tomakehurst', name: 'wiremock-jre8', version: project['com.github.tomakehurst.wiremock-jre8.version'])
    testCompile(group: 'org.glassfish.jersey.core', name: 'jersey-common', version: project['org.glassfish.jersey.core.version'])
    testCompile(group: 'org.slf4j', name: 'slf4j-log4j12', version: project['org.slf4j.version'])
    testCompile(group: 'io.opentracing', name:'opentracing-mock', version: project['io.opentracing.version'])
    testCompile(group: 'io.opentracing', name:'opentracing-util', version: project['io.opentracing.version'], classifier: 'tests')
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'dk.bankdata.jaxrs'
            artifactId 'security'
            version version

            from components.java

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.hasProperty('ossrhUsername') ? ossrhUsername : ''
                password project.hasProperty('ossrhPassword') ? ossrhPassword : ''
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name 'Jaxrs Security utility'
            description 'JAX-RS Security utilities for use in implementing financial APIs'
            url 'https://github.com/Bankdata/jaxrs-security'
            scm {
                connection 'scm:git:https://github.com/Bankdata/jaxrs-security.git'
                developerConnection 'scm:git:https://github.com/Bankdata/jaxrs-security.git'
                url 'https://github.com/Bankdata/jaxrs-security.git'
                tag 'HEAD'
            }
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            developers {
                developer {
                    id 'bogedal'
                    name 'Kenneth Bøgedal'
                    email 'kbh@bankdata.dk'
                    organization 'Bankdata'
                    organizationUrl 'https://www.bankdata.dk'
                }
                developer {
                    id 'langecode'
                    name 'Thor Anker Kvisgård Lange'
                    email 'tel@bankdata.dk'
                    organization 'Bankdata'
                    organizationUrl 'https://www.bankdata.dk'
                }
            }
        }
    }
}
