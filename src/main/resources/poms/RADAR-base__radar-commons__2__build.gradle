/*
 * Copyright 2017 The Hyve and King's College London
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'application'

mainClassName = 'org.radarbase.mock.MockProducer'

configurations {
    applicationRuntimeOnly
}

run {
    classpath += configurations.applicationRuntimeOnly
    if (project.hasProperty("mockConfig")) {
        args project.property("mockConfig")
    } else {
        args 'mock.yml'
    }
}

description = 'RADAR Common testing library mocking code and utilities.'

targetCompatibility = '11.0'
sourceCompatibility = '11.0'

repositories {
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

dependencies {
    api project(':radar-commons')
    api project(':radar-commons-server')
    api group: 'org.apache.avro', name: 'avro', version: avroVersion
    api group: 'org.radarbase', name: 'radar-schemas-commons', version: radarSchemasVersion

    implementation group: 'com.opencsv', name: 'opencsv', version: '5.4'
    implementation group: 'com.fasterxml.jackson.core' , name: 'jackson-databind' , version: jacksonVersion
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: kafkaVersion
    implementation (group: 'io.confluent', name: 'kafka-avro-serializer', version: confluentVersion) {
        exclude group: 'com.101tec'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    applicationRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion

    // Direct producer uses KafkaAvroSerializer if initialized
    testImplementation group: 'junit', name: 'junit', version: junitVersion
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: hamcrestVersion
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
}

apply from: '../gradle/publishing.gradle'
apply from: '../gradle/codestyle.gradle'
