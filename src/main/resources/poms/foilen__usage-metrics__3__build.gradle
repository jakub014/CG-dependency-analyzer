apply plugin: 'maven'
apply plugin: 'signing'

dependencies {

  // Tools
  compile 'com.google.guava:guava:30.1-jre'

  // For jl-smalltools
  compile 'ch.qos.logback:logback-classic:1.2.3'
  compile "com.foilen:jl-smalltools:$smalltoolsVersion"
  compile 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
  compile "javax.xml.bind:jaxb-api:2.3.1"
  compile 'org.apache.commons:commons-lang3:3.10'
  compile 'org.slf4j:slf4j-api:1.7.30'

  // For API
  compile 'org.springframework:spring-web:5.2.7.RELEASE'

}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, sourcesJar	
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask(":uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      pom.project {
        name = "usage-metrics-api"
        packaging 'jar'
        artifactId 'usage-metrics-api'
        description 'To report usage metrics of different resources on a machine'
        url 'https://github.com/foilen/usage-metrics'

        scm {
          connection = "scm:git:git@github.com:foilen/usage-metrics-api.git"
          url = "https://github.com/foilen/usage-metrics"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id 'foilen'
            name 'Simon Levesque'
            email 'simon@foilen.com'
          }
        }
      }
    }
  }
}
