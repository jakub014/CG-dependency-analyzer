group 'com.tylerhoersch.lingo24'
version '1.2'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.5.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.5.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.5.3'
    compile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Lingo24-Java-SDK'
                packaging 'jar'
                description 'A Java client SDK for the Lingo24 translation API'
                url 'https://github.com/thoersch/lingo24-java-sdk'

                scm {
                    url 'https://github.com/thoersch/lingo24-java-sdk.git'
                    connection 'https://github.com/thoersch/lingo24-java-sdk.git'
                    developerConnection 'https://github.com/thoersch/lingo24-java-sdk.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'thoersch'
                        name 'Tyler Hoersch'
                    }
                    developer {
                        id 'tjharke13'
                        name 'Thomas Harke'
                    }
                    developer {
                        id 'jasenkoh'
                        name 'Jasenko Hadziomeragic'
                    }
                }
            }
        }
    }
}
