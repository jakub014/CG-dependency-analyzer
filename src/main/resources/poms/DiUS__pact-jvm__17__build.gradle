buildscript {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath 'org.apache.commons:commons-lang3:3.10'
    classpath 'org.clojure:clojure:1.10.1'
  }
}

plugins {
  id "nebula.clojure" version "9.4.1"
}

import org.apache.commons.lang3.RandomStringUtils

dependencies {
  api project(":provider")
  implementation 'org.clojure:clojure:1.10.1'
  implementation 'org.clojure:core.match:1.0.0'
  implementation 'org.clojure:core.rrb-vector:0.1.1'
  implementation 'leiningen-core:leiningen-core:2.9.4'
  implementation 'org.apache.maven:maven-aether-provider:3.0.5'
  implementation 'org.sonatype.aether:aether-connector-file:1.13.1'
  implementation 'org.sonatype.aether:aether-connector-wagon:1.13.1'
  implementation "org.apache.httpcomponents:httpclient:${project.httpClientVersion}"
  implementation "org.codehaus.groovy:groovy:${project.groovyVersion}"

  testImplementation 'org.clojure:tools.nrepl:0.2.13'
  testRuntime "org.junit.vintage:junit-vintage-engine:${project.junit5Version}"
  testImplementation('org.spockframework:spock-core:2.0-M4-groovy-3.0') {
    exclude group: 'org.codehaus.groovy'
  }

  groovyDoc "org.codehaus.groovy:groovy-all:${project.groovyVersion}"
}

clojure.aotCompile = true
clojureTest.junit = true
clojureRepl.port = '7888'

compileClojure {
  dependsOn compileGroovy
  classpath = classpath.plus(files(compileGroovy.destinationDir))
  destinationDir = file("${project.buildDir}/classes/java/main")
}

clojureTest {
  classpath = classpath.plus(files(compileGroovy.destinationDir))
  junitOutputDir = file("$buildDir/test-results/clojure/" + RandomStringUtils.randomAlphanumeric(6))
}

processResources {
  expand project.properties
}
