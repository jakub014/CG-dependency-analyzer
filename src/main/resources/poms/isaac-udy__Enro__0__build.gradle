buildscript {
    apply from: "$rootDir/common_dependencies.gradle"

    repositories {
        mavenLocal()
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath deps.android.gradle
        classpath deps.kotlin.gradle
        classpath deps.hilt.gradle
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
    }
}

subprojects {
    apply from: "$rootDir/common.gradle"
    apply from: "$rootDir/common_publish.gradle"
    apply from: "$rootDir/common_dependencies.gradle"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task updateVersion {
    doLast {
        if (!project.hasProperty("versionName")) {
            throw new IllegalStateException("The updateVersion task requires a versionName property to be passed as an argument")
        }
        def versionPropertiesFile = rootProject.file("version.properties")
        def existingProperties = new Properties()
        existingProperties.load(new FileInputStream(versionPropertiesFile))

        def versionName = project.getProperties().get("versionName")
        def versionCode = (existingProperties.versionCode as int) + 1

        if(versionName == existingProperties.versionName) {
            throw new IllegalStateException("The versionName '$versionName' is the current versionName")
        }

        versionPropertiesFile.write("versionName=$versionName\nversionCode=$versionCode")
    }
}