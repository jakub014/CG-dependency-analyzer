apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.tananaev'
version = '1.2'

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.5'
    testCompile 'junit:junit:4.12'
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'json-patch'
                    packaging 'jar'
                    description 'A Gson-based JSON patch standard implementation.'
                    url 'https://github.com/tananaev/json-patch'

                    scm {
                        connection 'scm:git:https://github.com/tananaev/json-patch'
                        developerConnection 'scm:git:git@github.com:tananaev/json-patch.git'
                        url 'https://github.com/tananaev/json-patch'
                    }

                    licenses {
                        license {
                            name 'Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    developers {
                        developer {
                            id 'tananaev'
                            name 'Anton Tananaev'
                            email 'anton.tananaev@gmail.com'
                        }
                    }
                }
            }
        }
    }

}
