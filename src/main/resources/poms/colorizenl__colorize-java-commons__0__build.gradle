apply plugin: "java-library"
apply plugin: "jacoco"

group = "nl.colorize"
version = "2021.4"
sourceCompatibility = "11"
targetCompatibility = "11"
compileJava.options.encoding = "UTF-8"
sourceSets.main.java.srcDirs = ["source"]
sourceSets.main.resources.srcDirs = ["resources"]
sourceSets.test.java.srcDirs = ["test"]

repositories {
    mavenCentral()
}

dependencies {
    api "com.google.guava:guava:30.1-jre"
    api "com.google.code.gson:gson:2.8.6"
    api "org.jdom:jdom2:2.0.6"
    api "jaxen:jaxen:1.2.0"
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/swing/**"])
        })
    }
}

task coverage(dependsOn: ["test", "jacocoTestReport"]) {
    doLast {
        def report = file("${buildDir}/reports/jacoco/test/html/index.html").text
        def coverage = report =~ "tfoot.*?ctr2.*?>([\\d.]+%).*?tfoot"
        println "-" * 40
        println "Test coverage: " + coverage[0][1]
    }
}
