
import org.apache.tools.ant.filters.*

apply plugin: 'java'

dependencies {
  implementation project(':core')
  implementation project(':dcs:model')

  implementation "com.fasterxml.jackson.core:jackson-databind"
  implementation "org.apache.httpcomponents:httpclient"
  implementation "com.carrotsearch.console:launcher"
}

configurations {
  distribution
}

task buildDistribution(type: Sync) {
  def antPrj = new org.apache.tools.ant.Project()
  afterEvaluate {
    replaceTokens.each { k, v -> antPrj.setProperty(k.toString(), v.toString()) }
  }

  inputs.properties replaceTokensTrigger

  doFirst {
    antPrj.setProperty("version.jackson-databind", getVersion("com.fasterxml.jackson.core:jackson-databind"))
    antPrj.setProperty("version.httpclient", getVersion("org.apache.httpcomponents:httpclient"))
    antPrj.setProperty("version.launcher", getVersion("com.carrotsearch.console:launcher"))
  }

  from(project.projectDir, {
    include 'src/**'
    exclude 'src/main/assembly/**'
  })

  from(file("src/main/assembly"), {
    include '**/*'
    filteringCharset = 'UTF-8'
    filter(ExpandProperties, project: antPrj)
  })

  into "$buildDir/distribution"
}

artifacts {
  distribution file("$buildDir/distribution"), {
    builtBy tasks.buildDistribution
  }
}

