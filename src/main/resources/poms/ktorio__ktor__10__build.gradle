description = ''

kotlin.sourceSets {
    jvmMain.dependencies {
        api project(':ktor-server:ktor-server-host-common')

        api group: 'io.netty', name: 'netty-codec-http2', version: netty_version
        api group: 'org.eclipse.jetty.alpn', name: 'alpn-api', version: jetty_alpn_api_version

        api group: 'io.netty', name: 'netty-transport-native-kqueue', version: netty_version
        api group: 'io.netty', name: 'netty-transport-native-epoll', version: netty_version
    }
    jvmTest.dependencies {
        api project(':ktor-server:ktor-server-test-host')
        api project(':ktor-server:ktor-server-test-suites')
        api project(':ktor-server:ktor-server-core')

        api group: 'io.netty', name: 'netty-tcnative', version: netty_tcnative_version
        api group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: netty_tcnative_version
        implementation "io.mockk:mockk:$mockk_version"
    }
}


def enableAlpnProp = project.hasProperty('enableAlpn')
if (enableAlpnProp) {
    def nativeClassifier;
    def osName = System.getProperty('os.name').toLowerCase()

    if (osName.contains('win')) {
        nativeClassifier = 'windows-x86_64'
    } else if (osName.contains('linux')) {
        nativeClassifier = 'linux-x86_64'
    } else if (osName.contains('mac')) {
        nativeClassifier = 'osx-x86_64'
    } else {
        throw new InvalidUserDataException("Unsupoprted os family $osName")
    }

    dependencies {
        compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: netty_tcnative_version, classifier: nativeClassifier
    }
}

dependencies {
    jvmTestApi project(path: ':ktor-server:ktor-server-core', configuration: 'testOutput')
}
