buildscript {
  if ("$gradle.gradleVersion" != "6.7") {
    throw new GradleException("Gradle version 6.7 is required (received $gradle.gradleVersion)")
  }

  // https://github.com/gradle/gradle/issues/11308#issuecomment-554317655
  if (System.properties['org.gradle.internal.publish.checksums.insecure'] != 'true') {
    throw new GradleException("org.gradle.internal.publish.checksums.insecure must be 'true'")
  }

  apply from: file("$rootDir/build_libraries.gradle")

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:5.3.0"
    classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
    classpath "digital.wup:android-maven-publish:3.6.3"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.android.tools.build:gradle:4.1.3"
    classpath "de.undercouch:gradle-download-task:4.1.1"
    classpath "de.mannodermaus.gradle.plugins:android-junit5:1.7.1.1"
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.33.0"
  id "com.vanniktech.android.junit.jacoco" version "0.16.0"
  id "ru.vyarus.animalsniffer" version "1.5.1"
  id "ca.cutterslade.analyze" version "1.4.3"
  id "maven-publish"
}

ext {
  androidBuildToolsVersion = "29.0.2"
  androidCompileSDKVersion = 28
  androidMinimumSDKVersion = 21
  androidTargetSDKVersion = 28

  if (!project.hasProperty("mavenCentralUsername")) {
    logger.warn("No mavenCentralUsername property specified: Using an empty value")
    mavenCentralUsername = ""
  }
  if (!project.hasProperty("mavenCentralPassword")) {
    logger.warn("No mavenCentralPassword property specified: Using an empty value")
    mavenCentralPassword = ""
  }

  if (project.hasProperty("org.librarysimplified.directory.publish")) {
    useDirectoryPublishPath = project.property("org.librarysimplified.directory.publish")
    useDirectoryPublish = true
    logger.warn("org.librarysimplified.directory.publish is set: We will publish artifacts to ${useDirectoryPublishPath}")
  } else {
    logger.warn("org.librarysimplified.directory.publish is not set: We will not publish artifacts to a custom directory")
    useDirectoryPublish = false
    useDirectoryPublishPath = null
  }

  if (project.hasProperty("org.librarysimplified.no_signing")) {
    disableSigning = true
    logger.warn("org.librarysimplified.no_signing is set: We will not sign artifacts")
  } else {
    disableSigning = false
    logger.warn("org.librarysimplified.no_signing is not set: We will attempt to sign artifacts")
  }
}

apply plugin: "com.vanniktech.android.junit.jacoco"
apply plugin: "de.undercouch.download"

allprojects { project ->
  group = project.ext["GROUP"]
  version = project.ext["VERSION_NAME"]

  apply plugin: 'ca.cutterslade.analyze'
}

// Configure all projects
subprojects { project ->

  switch (POM_PACKAGING) {
    case "jar":
      logger.info("Configuring ${project} ${version} (${POM_PACKAGING}) as jar project")

      apply plugin: "java"
      apply plugin: "java-library"
      apply plugin: "kotlin"

      /*
       * Apply the Animal Sniffer plugin to check that code is Android compatible.
       */

      apply plugin: "ru.vyarus.animalsniffer"

      dependencies {
        signature "org.codehaus.mojo.signature:java16:1.1@signature"
        signature "net.sf.androidscents.signature:android-api-level-21:5.0.1_r2@signature"
      }

      /*
       * Build an OSGi bundle.
       */

      apply plugin: "biz.aQute.bnd.builder"

      jar {
        bnd """
Automatic-Module-Name: ${POM_AUTOMATIC_MODULE_NAME}

-removeheaders: Bnd-LastModified, Tool, Private-Package
"""
      }
      break

    case "apk":
      logger.info("Configuring ${project} ${version} (${POM_PACKAGING}) as Android application project")

      apply plugin: "com.android.application"
      apply plugin: "kotlin-android"

      android {
        compileSdkVersion androidCompileSDKVersion
        buildToolsVersion androidBuildToolsVersion

        defaultConfig {
          minSdkVersion androidMinimumSDKVersion
          targetSdkVersion androidTargetSDKVersion
        }
        compileOptions {
          encoding 'UTF-8'
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }
      }
      break

    case "aar":
      logger.info("Configuring ${project} ${version} (${POM_PACKAGING}) as Android library project")

      apply plugin: "com.android.library"
      apply plugin: "kotlin-android"
      apply plugin: "de.mannodermaus.android-junit5"

      android {
        compileSdkVersion androidCompileSDKVersion
        buildToolsVersion androidBuildToolsVersion

        defaultConfig {
          minSdkVersion androidMinimumSDKVersion
          targetSdkVersion androidTargetSDKVersion
          testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
          useLibrary 'org.apache.http.legacy'
        }
        compileOptions {
          encoding 'UTF-8'
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }
        testOptions {
          junitPlatform {
            releaseFilters {
              excludePattern "**/**"
            }
          }
          unitTests.includeAndroidResources = true
          unitTests.all {
            reports {
              junitXml.enabled = true
              html.enabled = true
            }
          }
        }
      }

      afterEvaluate {
        def scandoVersion = "0.0.5"
        def scandoSHA256 = "d1e6e57a791363d2a6e9930a15b779fb45855006f5ee7e0119286652ccad7827"
        def scandoSource = "https://repo1.maven.org/maven2/com/io7m/scando/com.io7m.scando.cmdline/${scandoVersion}/com.io7m.scando.cmdline-${scandoVersion}-main.jar"
        def scandoBinDir = "${project.buildDir}/bin/"
        def scandoTarget = "${scandoBinDir}/scando.jar"

        task scandoMakeDirectories() {
          mkdir "${scandoBinDir}"
        }

        task scandoDownload(type: Download, dependsOn: scandoMakeDirectories) {
          src scandoSource
          dest scandoTarget
          overwrite true
        }

        task scandoDownloadVerify(type: Verify, dependsOn: scandoDownload) {
          src scandoTarget
          checksum scandoSHA256
          algorithm 'SHA-256'
        }

        task scandoAnalyze(type: Exec, dependsOn: [scandoDownloadVerify, assembleDebug]) {
          def oldGroup =
            "${project.group}".replace('.', '/')
          def oldPath =
            "${oldGroup}/${POM_ARTIFACT_ID}/${VERSION_PREVIOUS}/${POM_ARTIFACT_ID}-${VERSION_PREVIOUS}.aar"
          def oldUrl =
            "https://repo1.maven.org/maven2/${oldPath}"

          commandLine = [
            "java",
            "-jar",
            scandoTarget,
            "--excludeList",
            "${project.rootDir}/VERSIONING.txt",
            "--oldJarUri",
            "${oldUrl}",
            "--oldJarVersion",
            "${VERSION_PREVIOUS}",
            "--ignoreMissingOld",
            "--newJar",
            "${buildDir}/outputs/aar/${POM_ARTIFACT_ID}-debug.aar",
            "--newJarVersion",
            "${VERSION_NAME}",
            "--textReport",
            "${project.buildDir}/scando-report.txt",
            "--htmlReport",
            "${project.buildDir}/scando-report.html"
          ]
        }

        task verifySemanticVersioning(dependsOn: [scandoAnalyze]) {

        }
      }
      break

    default:
      throw new IllegalStateException(
        "Unrecognized packaging type ${POM_PACKAGING} for ${project}")
  }

  /*
   * Configure publishing for the various project types.
   */

  switch (POM_PACKAGING) {
    case "jar":
      apply from: file("$rootDir/build_publishing.gradle")
      break

    case "apk":
      break

    case "aar":
      apply from: file("$rootDir/build_publishing.gradle")
      break

    default:
      throw new IllegalStateException(
        "Unrecognized packaging type ${POM_PACKAGING} for ${project}")
  }

  repositories {
    mavenLocal()
    mavenCentral()
    google()

    // Sonatype snapshots
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    maven { url "https://jitpack.io" }
    jcenter()
  }
}

/*
 * Add ktlint tasks.
 */

apply from: file("$rootDir/build_ktlint.gradle")

subprojects {
  assemble.dependsOn ktlint
}
