plugins {
   id 'java-library'
   id 'antlr'
   id 'maven-publish'
   id 'de.clashsoft.gentreesrc-gradle' version '0.5.0'
   // https://plugins.gradle.org/plugin/com.bmuschko.nexus
   id 'com.bmuschko.nexus' version '2.3.1'
   // https://plugins.gradle.org/plugin/io.codearte.nexus-staging
   id 'io.codearte.nexus-staging' version '0.22.0'
}

apply from: 'gradle/functional-test.gradle'

// --------------- Info ---------------

group = 'org.fulib'
version = 'git describe --tags'.execute([], rootDir).text[1..-2] // strip v and \n
description = 'A compiler for textual example scenarios.'

modifyPom {
   project {
      name = project.name
      description = project.description
      url = 'https://github.com/fujaba/fulibScenarios'
      inceptionYear = '2019'

      scm {
         url = 'https://github.com/fujaba/fulibScenarios'
      }

      licenses {
         license {
            name = 'BSD 3-Clause License'
            url = 'https://opensource.org/licenses/BSD-3-Clause'
         }
      }

      developers {
         developer {
            id = 'Clashsoft'
            name = 'Adrian Kunz'
            email = 'clashsoft@hotmail.com'
         }

         developer {
            id = 'azuendorf'
            name = 'Albert ZÃ¼ndorf'
            email = 'zuendorf@uni-kassel.de'
         }
      }
   }
}

sourceCompatibility = 1.8

// --------------- Dependencies ---------------

repositories {
   mavenLocal()
   mavenCentral()
}

dependencies {
   // --------------- Build Tools ---------------

   // https://mvnrepository.com/artifact/de.clashsoft/gentreesrc
   gentreesrc group: 'de.clashsoft', name: 'gentreesrc', version: '0.10.2'

   // https://mvnrepository.com/artifact/org.antlr/antlr4
   antlr group: 'org.antlr', name: 'antlr4', version: '4.9.1'

   // --------------- Fulib Libraries ---------------

   // https://mvnrepository.com/artifact/org.fulib/fulib
   api group: 'org.fulib', name: 'fulib', version: '1.5.1'

   // https://mvnrepository.com/artifact/org.fulib/fulibTools
   api group: 'org.fulib', name: 'fulibTools', version: '1.5.1'

   // --------------- Other Libraries ---------------

   // https://mvnrepository.com/artifact/org.apache.commons/commons-text
   implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'

   // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
   implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.9.1'

   // https://mvnrepository.com/artifact/commons-cli/commons-cli
   implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'

   // https://mvnrepository.com/artifact/org.ow2.asm/asm
   implementation group: 'org.ow2.asm', name: 'asm', version: '8.0.1'

   // --------------- Test Dependencies ---------------

   // https://mvnrepository.com/artifact/junit/junit
   testImplementation group: 'junit', name: 'junit', version: '4.13'

   // https://mvnrepository.com/artifact/org.fulib/fulibMockups
   testImplementation group: 'org.fulib', name: 'fulibMockups', version: '0.3.1'

   // https://mvnrepository.com/artifact/org.fulib/fulibTables
   testImplementation group: 'org.fulib', name: 'fulibTables', version: '1.4.0'
}

// --------------- Test Logging ---------------

tasks.withType(Test) {
   testLogging {
      showStandardStreams true
      exceptionFormat 'FULL'
   }
}

// --------------- ANTLR ---------------

def generatedANTLRDir = "$buildDir/generated-src/antlr/main/"

sourceSets.main.java.srcDir(generatedANTLRDir)

// https://stackoverflow.com/questions/40995727/gradle-cant-find-antlr-token-file
generateGrammarSource {
   def packageName = 'org.fulib.scenarios.parser'
   arguments += [ '-package', packageName ]
   outputDirectory = file("$generatedANTLRDir/${ packageName.replace('.', '/') }/")
}

// https://github.com/gradle/gradle/issues/820
configurations.compile {
   extendsFrom = extendsFrom.findAll { it != configurations.antlr }
}

// --------------- GenTreeSrc ---------------

gentreesrcJava {
   visitDefault = true
   visitParent = true
}
