/*
 * Copyright (c) 2019 Applivery
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins { id 'com.github.dcendents.android-maven' version '2.1' }
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: '../dependencies.gradle'
apply from: rootProject.file('gradle/checkstyle.gradle')

def STRING = "String"
def BOOLEAN = "Boolean"

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion versions.minSdk
        versionName version

        buildConfigField BOOLEAN, "CHECK_FOR_UPDATES_BACKGROUND", 'false'
    }

    buildTypes {
        debug {
            buildConfigField STRING, "API_URL", '"https://sdk-api.applivery.io"'
            buildConfigField STRING, "DOWNLOAD_API_URL", '"https://sdk-api.applivery.io"'
            testCoverageEnabled true
        }

        prerelease {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true

            buildConfigField STRING, "API_URL", '"https://sdk-api.applivery.io"'
            buildConfigField STRING, "DOWNLOAD_API_URL", '"https://download-api.applivery.io/"'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField STRING, "API_URL", '"https://sdk-api.applivery.io"'
            buildConfigField STRING, "DOWNLOAD_API_URL", '"https://download-api.applivery.io/"'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "androidx.appcompat:appcompat:${appcompatVersion}"
    implementation "com.google.android.material:material:${androidMaterialVersion}"

    // Network dependencies
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${loggingInterceptorVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"

    implementation "com.karumi:dexter:${dexterVersion}"

    implementation(project(":applivery-base"))
    implementation(project(":applivery-updates"))
    api(project(":applivery-data"))

    // Test dependencies
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-all:${mockitoVersion}"
    testImplementation "com.squareup.okhttp:mockwebserver:${mockwebserverVersion}"
}

install {
    repositories.mavenInstaller {
        pom.project {
            groupId project.group
            artifactId 'applivery-sdk'
            packaging 'aar'

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

apply from: 'deploy.gradle'
