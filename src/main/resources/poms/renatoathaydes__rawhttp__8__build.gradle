buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
    }
}

plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm"
    id 'maven'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

description = 'RawHTTP Cookies Integration'
version = rawHttpCookiesVersion

dependencies {
    implementation project(':rawhttp-core')
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "io.kotlintest:kotlintest:2.0.7"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'rawhttp.cookies',
                'Implementation-Version': rawHttpCookiesVersion,
                'Implementation-Vendor': 'Renato Athaydes'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

/* Publishing config */

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure(install.repositories.mavenInstaller) {
    pom.project {
        inceptionYear '2020'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://renatoathaydes.github.io/rawhttp'

        scm {
            connection 'git@github.com:renatoathaydes/rawhttp.git'
            developerConnection 'git@github.com:renatoathaydes/rawhttp.git'
            url 'https://github.com/renatoathaydes/rawhttp'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

final rawHttpGradle = new RawHttpGradle(project)

bintray {
    user = rawHttpGradle.getGradleProperty("bintrayUserName")
    key = rawHttpGradle.getGradleProperty("bintrayApiKey")
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'rawhttp-cookies'
        licenses = ['Apache-2.0']
        desc = project.description
        vcsUrl = 'https://github.com/renatoathaydes/rawhttp.git'
        websiteUrl = 'https://renatoathaydes.github.io/rawhttp/rawhttp-modules/cookies.html'
        issueTrackerUrl = 'https://github.com/renatoathaydes/rawhttp/issues'
        labels = ['http', 'java', 'cookies']
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty("ossrhUsername") ? ossrhUsername : null
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : null
                close = '1' // '0' to NOT close
            }
        }
    }

}

bintrayUpload.dependsOn build, sourcesJar
