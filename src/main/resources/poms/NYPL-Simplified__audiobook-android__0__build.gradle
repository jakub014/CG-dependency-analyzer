buildscript {
  if ("$gradle.gradleVersion" != "6.7") {
    throw new GradleException("Gradle version 6.7 is required (received $gradle.gradleVersion)")
  }

  // https://github.com/gradle/gradle/issues/11308#issuecomment-554317655
  if (System.properties['org.gradle.internal.publish.checksums.insecure'] != 'true') {
    throw new GradleException("org.gradle.internal.publish.checksums.insecure must be 'true'")
  }

  ext.kotlin_version = "1.4.20"

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:4.1.1"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "digital.wup:android-maven-publish:3.6.3"
    classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
    classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:5.2.0"
    classpath "de.undercouch:gradle-download-task:4.1.1"
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.28.0"
  id "com.vanniktech.android.junit.jacoco" version "0.16.0"
  id "digital.wup.android-maven-publish" version "3.6.3"
  id "ru.vyarus.animalsniffer" version "1.5.0"
}

ext {
  androidBuildToolsVersion = "29.0.2"
  androidCompileSDKVersion = 28
  androidMinimumSDKVersion = 21
  androidTargetSDKVersion = 28

  if (!project.hasProperty("mavenCentralUsername")) {
    logger.warn("No mavenCentralUsername property specified: Using an empty value")
    mavenCentralUsername = ""
  }
  if (!project.hasProperty("mavenCentralPassword")) {
    logger.warn("No mavenCentralPassword property specified: Using an empty value")
    mavenCentralPassword = ""
  }

  if (project.hasProperty("org.librarysimplified.directory.publish")) {
    useDirectoryPublishPath = project.property("org.librarysimplified.directory.publish")
    useDirectoryPublish = true
    logger.warn("org.librarysimplified.directory.publish is set: We will publish artifacts to ${useDirectoryPublishPath}")
  } else {
    logger.warn("org.librarysimplified.directory.publish is not set: We will not publish artifacts to a custom directory")
    useDirectoryPublish = false
    useDirectoryPublishPath = null
  }

  if (project.hasProperty("org.librarysimplified.no_signing")) {
    disableSigning = true
    logger.warn("org.librarysimplified.no_signing is set: We will not sign artifacts")
  } else {
    disableSigning = false
    logger.warn("org.librarysimplified.no_signing is not set: We will attempt to sign artifacts")
  }

  irradiaFieldrushVersion = "0.0.16"
  irradiaMimeVersion = "1.1.1"
}

subprojects {
  // Force version 1.0.2 of appcompat. A bug in 1.1.0 causes a crash on API 21.
  //
  // https://issuetracker.google.com/issues/141132133#comment42
  //
  // TODO: Remove once appcompat:1.2.0 is available.
  configurations.all {
    resolutionStrategy.force 'androidx.appcompat:appcompat:1.0.2'
  }
}

ext.libraries = [
  androidXAppCompat       : "androidx.appcompat:appcompat:1.0.2",
  androidXRecyclerView    : "androidx.recyclerview:recyclerview:1.1.0",
  androidXTestEspresso    : "androidx.test.espresso:espresso-core:3.3.0-alpha02",
  androidXTestExtJunit    : "androidx.test.ext:junit:1.1.2-alpha02",
  androidXTestOrchestrator: "androidx.test:orchestrator:1.1.0",
  androidXTestRules       : "androidx.test:rules:1.1.0",
  androidXTestRunner      : "androidx.test:runner:1.1.0",
  googleExoplayer         : "com.google.android.exoplayer:exoplayer:r1.5.15",
  googleGuava             : "com.google.guava:guava:27.1-android",
  irradiaFieldrushAPI     : "one.irradia.fieldrush:one.irradia.fieldrush.api:${irradiaFieldrushVersion}",
  irradiaFieldrushVanilla : "one.irradia.fieldrush:one.irradia.fieldrush.vanilla:${irradiaFieldrushVersion}",
  irradiaMimeAPI          : "one.irradia.mime:one.irradia.mime.api:${irradiaMimeVersion}",
  irradiaMimeVanilla      : "one.irradia.mime:one.irradia.mime.vanilla:${irradiaMimeVersion}",
  jacksonCore             : "com.fasterxml.jackson.core:jackson-core:2.10.2",
  jacksonDatabind         : "com.fasterxml.jackson.core:jackson-databind:2.10.2",
  jcip                    : "com.io7m.jcip:jcip-annotations:1.0.2",
  jodaTime                : "joda-time:joda-time:2.10.5",
  junit                   : "junit:junit:4.12",
  kotlinStdlib            : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
  kotlinReflect           : "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
  logbackAndroid          : "com.github.tony19:logback-android:2.0.0",
  logbackClassic          : "ch.qos.logback:logback-classic:1.3.0-alpha5",
  mockito                 : "org.mockito:mockito-core:2.27.0",
  nimbusJoseJWT           : "com.nimbusds:nimbus-jose-jwt:8.20",
  okhttp                  : "com.squareup.okhttp3:okhttp:3.14.1",
  quicktheories           : 'org.quicktheories:quicktheories:0.26',
  rxjava                  : "io.reactivex:rxjava:1.3.8",
  slf4j                   : "org.slf4j:slf4j-api:2.0.0-alpha1",
]

apply plugin: "com.vanniktech.android.junit.jacoco"
apply plugin: "de.undercouch.download"

allprojects {
  group = project.ext["GROUP"]
  version = project.ext["VERSION_NAME"]
}

subprojects { project ->

  switch (POM_PACKAGING) {
    case "jar":
      logger.info("Configuring ${project} ${version} (${POM_PACKAGING}) as jar project")

      apply plugin: "java"
      apply plugin: "java-library"
      apply plugin: "kotlin"

      /*
       * Apply the Animal Sniffer plugin to check that code is Android compatible.
       */

      apply plugin: "ru.vyarus.animalsniffer"

      dependencies {
        signature "org.codehaus.mojo.signature:java16:1.1@signature"
        signature "net.sf.androidscents.signature:android-api-level-21:5.0.1_r2@signature"
      }

      /*
       * Build an OSGi bundle.
       */

      apply plugin: "biz.aQute.bnd.builder"

      jar {
        bnd """
Automatic-Module-Name: ${POM_AUTOMATIC_MODULE_NAME}

-removeheaders: Bnd-LastModified, Tool, Private-Package
"""
      }
      break

    case "apk":
      logger.info("Configuring ${project} ${version} (${POM_PACKAGING}) as Android application project")

      apply plugin: "com.android.application"
      apply plugin: "kotlin-android"

      android {
        compileSdkVersion androidCompileSDKVersion
        buildToolsVersion androidBuildToolsVersion

        defaultConfig {
          minSdkVersion androidMinimumSDKVersion
          targetSdkVersion androidTargetSDKVersion
        }
        compileOptions {
          encoding 'UTF-8'
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }
      }
      break

    case "aar":
      logger.info("Configuring ${project} ${version} (${POM_PACKAGING}) as Android library project")

      apply plugin: "com.android.library"
      apply plugin: "kotlin-android"

      android {
        compileSdkVersion androidCompileSDKVersion
        buildToolsVersion androidBuildToolsVersion

        defaultConfig {
          minSdkVersion androidMinimumSDKVersion
          targetSdkVersion androidTargetSDKVersion
          testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
          useLibrary 'org.apache.http.legacy'
        }
        compileOptions {
          encoding 'UTF-8'
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }
        testOptions {
          execution 'ANDROIDX_TEST_ORCHESTRATOR'
          animationsDisabled = true
        }
      }

      afterEvaluate {
        def scandoVersion = "0.0.5"
        def scandoSHA256 = "d1e6e57a791363d2a6e9930a15b779fb45855006f5ee7e0119286652ccad7827"
        def scandoSource = "https://repo1.maven.org/maven2/com/io7m/scando/com.io7m.scando.cmdline/${scandoVersion}/com.io7m.scando.cmdline-${scandoVersion}-main.jar"
        def scandoBinDir = "${project.buildDir}/bin/"
        def scandoTarget = "${scandoBinDir}/scando.jar"

        task scandoMakeDirectories() {
          mkdir "${scandoBinDir}"
        }

        task scandoDownload(type: Download, dependsOn: scandoMakeDirectories) {
          src scandoSource
          dest scandoTarget
          overwrite true
        }

        task scandoDownloadVerify(type: Verify, dependsOn: scandoDownload) {
          src scandoTarget
          checksum scandoSHA256
          algorithm 'SHA-256'
        }

        task scandoAnalyze(type: Exec, dependsOn: [scandoDownloadVerify, assembleDebug]) {
          def oldGroup =
            "${project.group}".replace('.', '/')
          def oldPath =
            "${oldGroup}/${POM_ARTIFACT_ID}/${VERSION_PREVIOUS}/${POM_ARTIFACT_ID}-${VERSION_PREVIOUS}.aar"
          def oldUrl =
            "https://repo1.maven.org/maven2/${oldPath}"

          commandLine = [
            "java",
            "-jar",
            scandoTarget,
            "--excludeList",
            "${project.rootDir}/VERSIONING.txt",
            "--oldJarUri",
            "${oldUrl}",
            "--oldJarVersion",
            "${VERSION_PREVIOUS}",
            "--ignoreMissingOld",
            "--newJar",
            "${buildDir}/outputs/aar/${POM_ARTIFACT_ID}-debug.aar",
            "--newJarVersion",
            "${VERSION_NAME}",
            "--textReport",
            "${project.buildDir}/scando-report.txt",
            "--htmlReport",
            "${project.buildDir}/scando-report.html"
          ]
        }

        task verifySemanticVersioning(dependsOn: [scandoAnalyze]) {

        }
      }
      break

    default:
      throw new IllegalStateException(
        "Unrecognized packaging type ${POM_PACKAGING} for ${project}")
  }

  task javadocsJar(type: Jar) {
    classifier = "javadoc"
  }

  task sourcesJar(type: Jar) {
    classifier = "sources"
    from "src/main/java", "src/main/resources"
  }

  apply plugin: "digital.wup.android-maven-publish"

  publishing {
    publications {
      basicJar(MavenPublication) {

        switch (POM_PACKAGING) {
          case "jar":
            from components.java
            break

          case "apk":
            break

          case "aar":
            from components.android
            break
        }

        artifact sourcesJar
        artifact javadocsJar

        pom {
          artifactId = POM_ARTIFACT_ID
          name = POM_NAME
          packaging = POM_PACKAGING
          description = POM_DESCRIPTION
          url = POM_URL

          scm {
            connection = POM_SCM_CONNECTION
            developerConnection = POM_SCM_DEV_CONNECTION
            url = POM_SCM_URL
          }

          licenses {
            license {
              name = POM_LICENCE_NAME
              url = POM_LICENCE_URL
            }
          }

          developers {
            developer {
              id = POM_DEVELOPER_ID
              name = POM_DEVELOPER_NAME
              email = POM_DEVELOPER_EMAIL
              url = POM_DEVELOPER_URL
            }
          }
        }
      }
    }

    repositories {
      if (useDirectoryPublish) {
        logger.info("Using ${useDirectoryPublishPath} for publication")

        maven {
          name = "Directory"
          url = "file://${useDirectoryPublishPath}"
        }
      }

      if (version.endsWith("SNAPSHOT")) {
        logger.info("Using Sonatype snapshots repository for publication")
        maven {
          url = "https://oss.sonatype.org/content/repositories/snapshots/"
          credentials(PasswordCredentials) {
            username mavenCentralUsername
            password mavenCentralPassword
          }
        }
      }
    }
  }

  if (!disableSigning) {
    apply plugin: "signing"

    signing {
      useGpgCmd()
      sign publishing.publications.basicJar
    }
  }

  repositories {
    mavenLocal()
    mavenCentral()

    // Sonatype snapshots
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    jcenter()
    google()
  }
}

// Add ktlint tasks to root project. This executes faster than configuring
// the tasks for each subproject individually.

apply from: file("$rootDir/ktlint.gradle")

subprojects {
  assemble.dependsOn ktlint
}
