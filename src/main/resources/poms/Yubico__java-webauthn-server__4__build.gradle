plugins {
  id 'java'
  id 'war'
  id 'application'
  id 'scala'
  id 'io.github.cosmicsilence.scalafix'
}

description = 'WebAuthn demo'

configurations {
  forJdk10
}

dependencies {
  implementation(platform(rootProject))

  implementation(
    project(':webauthn-server-attestation'),
    project(':webauthn-server-core-minimal'),
    project(':yubico-util'),

    'com.google.guava:guava',
    'com.fasterxml.jackson.core:jackson-databind',
    'com.upokecenter:cbor',
    'javax.ws.rs:javax.ws.rs-api',
    'org.eclipse.jetty:jetty-server',
    'org.eclipse.jetty:jetty-servlet',
    'org.glassfish.jersey.containers:jersey-container-servlet-core',
    'org.slf4j:slf4j-api',
  )

  forJdk10(
    'javax.activation:activation',
    'javax.xml.bind:jaxb-api',
  )

  runtimeOnly(
    configurations.forJdk10,

    'ch.qos.logback:logback-classic',
    'org.glassfish.jersey.containers:jersey-container-servlet',
    'org.glassfish.jersey.inject:jersey-hk2',
  )

  testImplementation(
    project(':webauthn-server-core-minimal').sourceSets.test.output,
    project(':yubico-util-scala'),

    'junit:junit',
    'org.mockito:mockito-core',
    'org.scala-lang:scala-library',
    'org.scalacheck:scalacheck_2.13',
    'org.scalatest:scalatest_2.13',
  )

  modules {
    module('javax.servlet:servlet-api') {
      replacedBy('javax.servlet:javax.servlet-api')
    }
  }
}

mainClassName = 'demo.webauthn.EmbeddedServer'

[installDist, distZip, distTar].each { task ->
  def intoDir = (task == installDist) ? "/" : "${project.name}-${project.version}"
  task.into(intoDir) {
    from 'keystore.jks'
    from('src/main/webapp') {
      into 'src/main/webapp'
    }
  }
}
