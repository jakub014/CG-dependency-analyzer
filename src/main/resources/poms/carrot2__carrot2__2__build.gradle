
import org.apache.tools.ant.filters.*

apply plugin: 'java-library'

dependencies {
  implementation project(':core')
  testImplementation project(':lang:polish')
  testImplementation project(':lang:lucene')
  testImplementation "org.apache.lucene:lucene-analyzers-icu"
  testImplementation "junit:junit"
}

configurations {
  distribution
}

task buildDistribution(type: Sync) {
  def antPrj = new org.apache.tools.ant.Project()
  afterEvaluate {
    replaceTokens.each { k, v -> antPrj.setProperty(k.toString(), v.toString()) }
  }

  inputs.properties replaceTokensTrigger

  doFirst {
    antPrj.setProperty("version.lucene-analyzers-icu", getVersion("org.apache.lucene:lucene-analyzers-icu"))
    antPrj.setProperty("version.junit", getVersion("junit:junit"))
  }

  from(project.projectDir, {
    include 'src/**'
    exclude 'src/main/assembly/**'
    includeEmptyDirs false
    filteringCharset = 'UTF-8'
    filter(LineContains, negate: true, contains: ['fragment-start{'])
    filter(LineContains, negate: true, contains: ['fragment-end{'])
  })

  from(files("src/main/assembly"), {
    filteringCharset = 'UTF-8'
    filter(ExpandProperties, project: antPrj)
  })

  from(rootProject.projectDir, {
    include "gradlew*", "gradle/wrapper/**"
  })

  into "$buildDir/distribution"
}

artifacts {
  distribution file("$buildDir/distribution"), {
    builtBy tasks.buildDistribution
  }
}
